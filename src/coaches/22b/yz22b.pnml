// Begin YZ22B

// Graphics

// Purchase Menu
spriteset (spriteset_22b_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_22b_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "22b")
}

// Vehicle
spriteset (spriteset_22b_original) {
    template_empty ()
}
alternative_sprites(spriteset_22b_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "22b")
}

switch (FEAT_TRAINS, SELF, switch_yz22b_articulated_part, extra_callback_info1) {
    1..2: return yz22b;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_22b_graphics, position_in_articulated_veh % 3) {
    1: spriteset_22b_original;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_yz22b_capacity, position_in_articulated_veh % 3) {
    1: return 118;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_yz22b_additional_text, param_cargo_decay) {
    return string(STR_DESC_4, string(STR_FULL_NAME_YZ22B), string(STR_22B_LIVERY_AVAILABILITY), string(STR_COMFORT_144), string(STR_NO_AIR_CONDITIONER));
}


item (FEAT_TRAINS, yz22b) {
    property {
        // Menu
        name:                           string(STR_NAME_YZ22B);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1988,01,01);
        model_life:                     10;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         42.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                switch_yz22b_additional_text;
        can_attach_wagon:               locowagon;
        
        // Graphics
        default:                        switch_22b_graphics;
        purchase:                       spriteset_22b_purchase_original;
        articulated_part:               switch_yz22b_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 switch_yz22b_capacity;
        cargo_age_period:               (9 * 4 * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    6;
        running_cost_factor:            4 * runningcostfactor() * cafecostfactor(); // NO AC FUNCTION
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        118/3;
    }
}

// End YZ22B

