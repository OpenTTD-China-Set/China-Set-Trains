//Begin CA25K
switch (FEAT_TRAINS, SELF, switch_ca25k_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25K_SERIES);
    return CB_RESULT_NO_TEXT;
}


switch (FEAT_TRAINS, SELF, switch_ca25k_articulated_part, extra_callback_info1) {
    1..2: return ca25k;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Graphics

// Purchase Menu

spriteset (spriteset_ca25k_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25k", "ca25k")
}

// Vehicle
spriteset (spriteset_ca25k_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "ca25k")
}
spriteset (spriteset_ca25k_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "ca25k-green")
}
spriteset (spriteset_ca25k_gmsr, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "ca25k-gmsr")
}
spriteset (spriteset_ca25k_guangshen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "ca25k-guangshen")
}
switch (FEAT_TRAINS, SELF, switch_ca25k_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    2: string(STR_GAOZU_GREEN);
    3: return string(STR_GMSR);
    4: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca25k_graphics_subtype_year_depend, build_year) {
     0..2014: spriteset_ca25k_original;
    spriteset_ca25k_green;
}
switch (FEAT_TRAINS, SELF, switch_ca25k_graphics_subtype_0, param_gaozulv) {
    1: switch_ca25k_graphics_subtype_year_depend;
    spriteset_ca25k_original;
}

switch (FEAT_TRAINS, SELF, switch_ca25k_graphics_subtype, cargo_subtype) {
    0: switch_ca25k_graphics_subtype_0;
    1: spriteset_ca25k_original;
    2: spriteset_ca25k_green;
    3: spriteset_ca25k_gmsr;
    4: spriteset_ca25k_guangshen;
}

switch (FEAT_TRAINS, SELF, switch_ca25k_graphics, position_in_articulated_veh % 3) {
    1: switch_ca25k_graphics_subtype;
    empty_sprites;
}

//Cap

switch (FEAT_TRAINS, SELF, switch_ca25k_capacity, position_in_articulated_veh % 3) {
    1: return 48;
    return 0;
}


item (FEAT_TRAINS, ca25k) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25K);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2058;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1996,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         44.8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA25K), string(STR_CAFE_EFFECT),  string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVAILABLE), string(STR_ORIGINAL), string(STR_GAOZU_GREEN), string(STR_GMSR), string(STR_GUANGSHEN)), string(STR_COMFORT_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca25k_cargo_subtype;

        // Graphics
        default:                        switch_ca25k_graphics;
        purchase:                       spriteset_ca25k_purchase_original;
        articulated_part:               switch_ca25k_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25k_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    26;
        running_cost_factor:            5 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   70;
        purchase_cargo_capacity:        48/3;
        name:                            switch_ca25k_name;

    }
}

// End CA25K

