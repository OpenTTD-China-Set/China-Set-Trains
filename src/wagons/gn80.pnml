//Begin GN80
switch (FEAT_TRAINS, SELF, switch_gn80_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_TANKER);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_gn80_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "gn80")
}

// Vehicle
spriteset (spriteset_gn80_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "gn80")
}

random_switch (FEAT_TRAINS, SELF, random_gn80_colour) {
    1: spriteset_gn80_black; // More colours waiting to be added
}
switch (FEAT_TRAINS, SELF, switch_gn80_cargo_subtype, cargo_subtype) {
    0: return string(STR_RANDOM_COLOR);
    1: return string(STR_BLACK);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_gn80_graphics_subtype, cargo_subtype) {
    0: random_gn80_colour;
    1: spriteset_gn80_black;
}

switch (FEAT_TRAINS, SELF, switch_gn80_articulated_part, extra_callback_info1) {
    1..2: return gn80;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_gn80_graphics, position_in_articulated_veh % 3) {
    1: switch_gn80_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_gn80_capacity, position_in_articulated_veh % 3) {
    1: return 70;
    return 0;
}


item (FEAT_TRAINS, gn80) {
    property {
        // Menu
        name:                           string(STR_NAME_GN80);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3088;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2011,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_LIQUID, CC_HAZARDOUS);
        non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 70;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         25.6 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_GN80_LIVERY_AVAILABILITY), string(STR_FRESHNESS_MEDIUM, 185));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_gn80_cargo_subtype;

        // Graphics
        default:                        switch_gn80_graphics;
        purchase:                       spriteset_gn80_purchase;
        articulated_part:               switch_gn80_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_gn80_capacity;
        cargo_age_period:               185 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    6;
        running_cost_factor:            5 * runningcostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        81/3;
        name:                            switch_gn80_name;

    }
}

// End GN80

