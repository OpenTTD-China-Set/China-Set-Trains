//Begin RW25K
switch (FEAT_TRAINS, SELF, switch_rw25k_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25K_SERIES);
    return CB_RESULT_NO_TEXT;
}


switch (FEAT_TRAINS, SELF, switch_rw25k_articulated_part, extra_callback_info1) {
    1..2: return rw25k;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Graphics

// Purchase Menu

spriteset (spriteset_rw25k_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25k", "rw25k")
}

// Vehicle
spriteset (spriteset_rw25k_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k")
}
spriteset (spriteset_rw25k_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k-green")
}
spriteset (spriteset_rw25k_gmsr, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k-gmsr")
}
spriteset (spriteset_rw25k_guangshen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k-guangshen")
}
spriteset (spriteset_rw25k_lakeblue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k-lakeblue")
}
spriteset (spriteset_rw25k_int_vn, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25k", "rw25k-int-vn")
}

switch (FEAT_TRAINS, SELF, switch_rw25k_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    2: return string(STR_GAOZU_GREEN);
    3: return string(STR_GMSR);
    4: return string(STR_GUANGSHEN);
    5: return string(STR_LAKEBLUE);
    6: return string(STR_INTERNATIONAL_VN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_rw25k_graphics_subtype_year_depend, build_year) {
    0..2014: spriteset_rw25k_original; //Unfinished
    spriteset_rw25k_green;
}
switch (FEAT_TRAINS, SELF, switch_rw25k_graphics_subtype_0, param_gaozulv) {
    1: switch_rw25k_graphics_subtype_year_depend;
    spriteset_rw25k_original;
}

switch (FEAT_TRAINS, SELF, switch_rw25k_graphics_subtype, cargo_subtype) {
    0: switch_rw25k_graphics_subtype_0;
    1: spriteset_rw25k_original;
    2: spriteset_rw25k_green;
    3: spriteset_rw25k_gmsr;
    4: spriteset_rw25k_guangshen;
    5: spriteset_rw25k_lakeblue;
    6: spriteset_rw25k_int_vn;
}

switch (FEAT_TRAINS, SELF, switch_rw25k_graphics, position_in_articulated_veh % 3) {
    1: switch_rw25k_graphics_subtype;
    empty_sprites;
}

//Cap

switch (FEAT_TRAINS, SELF, switch_rw25k_capacity, position_in_articulated_veh % 3) {
    1: return 36;
    return 0;
}


item (FEAT_TRAINS, rw25k) {
    property {
        // Menu
        name:                           string(STR_NAME_RW25K);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2058;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1996,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 36;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               444 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         47.0 ton;
        cost_factor:                    13;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            11;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_RW25K), string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVAILABLE), string(STR_ORIGINAL), string(STR_GAOZU_GREEN), string(STR_GMSR), string(STR_GUANGSHEN), string(STR_INTERNATIONAL_VN)), string(STR_COMFORT_HIGH, 384));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_rw25k_cargo_subtype;

        // Graphics
        default:                        switch_rw25k_graphics;
        purchase:                       spriteset_rw25k_purchase_original;
        articulated_part:               switch_rw25k_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_rw25k_capacity;
        cargo_age_period:               (24 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    14;
        running_cost_factor:            13 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   52;
        purchase_cargo_capacity:        36/3;
        name:                            switch_rw25k_name;

    }
}

// End RW25K

