//Begin BEIJING_FREIGHT
switch (FEAT_TRAINS, SELF, switch_beijing_freight_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BEIJING_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_beijing_freight_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel/beijing", "beijing-freight")
}

//Adds Vehicle
spriteset (spriteset_beijing_freight, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel/beijing", "beijing-freight")
}

switch (FEAT_TRAINS, SELF, switch_beijing_freight_articulated_part, extra_callback_info1) {
    1..2: return beijing_freight;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}



switch (FEAT_TRAINS, SELF, switch_beijing_freight_graphics, position_in_articulated_veh % 3) {
    1: spriteset_beijing_freight;
    empty_sprites;
}


item (FEAT_TRAINS, beijing_freight) {
    property {
        // Menu
        name:                           string(STR_NAME_BEIJING_FREIGHT);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
/*         variant_group:					beijing_passenger; */

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1970,10,14);
        model_life:                     30;
        retire_early:                   17;
        vehicle_life:                   15;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          5400 hpM;
        weight:                         184 ton;
        tractive_effort_coefficient:    0.3367;
        cost_factor:                    1;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            1;
        reliability_decay:              25;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_BEIJING_FREIGHT), string(STR_ELECTRICITY_SUPPLY_NO), /* string(STR_BEIJING_FREIGHT_NICKNAME), */ string(STR_RELDEC_VERY_HIGH, 25));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_beijing_freight_graphics;
        purchase:                       spriteset_beijing_freight_purchase;
        articulated_part:               switch_beijing_freight_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 0;
        cost_factor:                    88;
        running_cost_factor:            74 * runningcostfactor();
        purchase_running_cost_factor:   296;
        purchase_cargo_capacity:        0;
        name:                           switch_beijing_freight_name;

    }
}

// End BEIJING_FREIGHT