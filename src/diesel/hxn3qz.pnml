switch (FEAT_TRAINS, SELF, switch_hxn3qz_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_HXN3QZ_FAMILY);
    return CB_RESULT_NO_TEXT;
}
// Begin HXN3QZ

// Graphics

// Purchase Menu
spriteset (spriteset_hxn3qz_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "hxn3qz")
}


// Vehicle
spriteset (spriteset_hxn3qz, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn3qz")
}
spriteset (spriteset_hxn3qz_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn3qz-back")
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_articulated_part, extra_callback_info1) {
    1..2: return hxn3qz;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_subtype_front, cargo_subtype) {
    0: spriteset_hxn3qz;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_subtype_back, cargo_subtype) {
    0: spriteset_hxn3qz_back;
}

// "Splitting HXN3QZ"

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_joint_front, position_in_vehid_chain % 6) {
    1: switch_hxn3qz_graphics_subtype_front;
    4: switch_hxn3qz_graphics_subtype_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_joint, position_in_vehid_chain_from_end) {
    1: switch_hxn3qz_graphics_subtype_back;
    switch_hxn3qz_graphics_joint_front;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_front, position_in_articulated_veh % 3) {
    1: switch_hxn3qz_graphics_subtype_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics_split, (position_in_consist_from_end == 1) && (num_vehs_in_consist > 3)) {
    1: switch_hxn3qz_graphics_subtype_back;
    switch_hxn3qz_graphics_front;
}

switch (FEAT_TRAINS, SELF, switch_hxn3qz_graphics, num_vehs_in_vehid_chain % 6) {
    0: switch_hxn3qz_graphics_joint;
    3: switch_hxn3qz_graphics_split;
    empty_sprites;
}


item (FEAT_TRAINS, hxn3qz) {
    property {
        // Menu
        name:                           string(STR_NAME_HXN3QZ);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        variant_group:					5148;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2014,1,1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          3300 kW;
        weight:                         150 ton;
        tractive_effort_coefficient:    0.422;
        cost_factor:                    93;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            35;
        reliability_decay:              8;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                        string(STR_DESC_3, string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_8), string(STR_HXN3QZ_CONSIST));
        can_attach_wagon:                       locowagon;
        cargo_subtype_text:                     switch_hxn3qz_cargo_subtype;

        // Graphics
        default:                                switch_hxn3qz_graphics;
        purchase:                               spriteset_hxn3qz_purchase;
        articulated_part:                       switch_hxn3qz_articulated_part;

        // Stats
        length:                                 switch_length_2_7_2;
        cargo_capacity:                         0;
        cost_factor:                            85;
        running_cost_factor:                    66 * runningcostfactor();
        purchase_running_cost_factor:           264;
        purchase_cargo_capacity:                0;
        name: switch_hxn3qz_name;

    }
}

// End HXN3QZ

