//Begin XL25T
switch (FEAT_TRAINS, SELF, switch_xl25t_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25T_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_xl25t_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25t/xl25t")
}

// Vehicle
spriteset (spriteset_xl25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/xl25t")
}
spriteset (spriteset_xl25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/xl25t-green")
}
spriteset (spriteset_bspxl25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bspxl25t")
}
spriteset (spriteset_bspxl25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bspxl25t-green")
}
spriteset (spriteset_xl25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/xl25t-xz")
}

switch (FEAT_TRAINS, SELF, switch_xl25t_articulated_part, extra_callback_info1) {
    1..2: return xl25t;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}


switch (FEAT_TRAINS, SELF, switch_xl25t_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    2: string(STR_BSP25T);
    3: string(STR_GAOZU_GREEN);
    4: string(STR_GAOZU_GREEN_BSP);
    5: string(STR_QINGZANG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_xl25t_graphics_subtype_year_depend, build_year) {
    0..2014: spriteset_xl25t_original;
    spriteset_xl25t_green;
}


switch (FEAT_TRAINS, SELF, switch_xl25t_graphics_subtype_0, param_gaozulv) {
    1: switch_xl25t_graphics_subtype_year_depend;
    spriteset_xl25t_original;
}

switch (FEAT_TRAINS, SELF, switch_xl25t_graphics_subtype, cargo_subtype) {
    0: switch_xl25t_graphics_subtype_0;
    1: spriteset_xl25t_original;
    2: spriteset_bspxl25t_original;
    3: spriteset_xl25t_green;
    4: spriteset_bspxl25t_green;
    5: spriteset_xl25t_qingzang;
}

switch (FEAT_TRAINS, SELF, switch_xl25t_graphics, position_in_articulated_veh % 3) {
    1: switch_xl25t_graphics_subtype;
    empty_sprites;
}


switch (FEAT_TRAINS, SELF, switch_xl25t_capacity, position_in_articulated_veh % 3) {
    1: return 71;
    return 0;
}


item (FEAT_TRAINS, xl25t) {
    property {
        // Menu
        name:                           string(STR_NAME_XL25T);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2052;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2003,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [MAIL, VALU, GOLD, DIAM];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             MAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 92;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          0 hp;
        weight:                         45.7 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            5;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_XL25T), string(STR_XL25T_LIVERY_AVAILABILITY), string(STR_FRESHNESS_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_xl25t_cargo_subtype;

        // Graphics
        default:                        switch_xl25t_graphics;
        purchase:                       spriteset_xl25t_purchase_original;
        articulated_part:               switch_xl25t_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_xl25t_capacity;
        cargo_age_period:               200 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    8;
        running_cost_factor:            7 * runningcostfactor();
        purchase_running_cost_factor:   28;
        purchase_cargo_capacity:        80/3;
        name:                           switch_xl25t_name;

    }
}

// End XL25T

