//Begin CA25G
switch (FEAT_TRAINS, SELF, switch_ca25g_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25G_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_ca25g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25g", "ca25g")
}

// Vehicle
spriteset (spriteset_ca25g_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g")
}
spriteset (spriteset_ca25g_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-green")
}
spriteset (spriteset_ca25g_orange, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-orange")
}
spriteset (spriteset_ca25g_jinwen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-jinwen")
}
spriteset (spriteset_ca25g_jinwen_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-jinwen-green")
}
spriteset (spriteset_ca25g_gmsr, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-gmsr")
}
spriteset (spriteset_ca25g_gmsr_early, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-gmsr-early")
}
spriteset (spriteset_ca25g_hejiu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-hejiu")
}
spriteset (spriteset_ca25g_fulong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "ca25g-fulong")
}
switch (FEAT_TRAINS, SELF, switch_ca25g_articulated_part, extra_callback_info1) {
    1..2: return ca25g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    2: return string(STR_GAOZU_GREEN);
    3: return string(STR_JINWEN);
    4: return string(STR_JINWEN_GREEN);
    5: return string(STR_ORANGE);
    6: return string(STR_GMSR);
    7: return string(STR_GMSR_EARLY);
    8: return string(STR_HEJIU);
    9: return string(STR_FULONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_graphics_subtype_year_depend, build_year) {
    0..2014: spriteset_ca25g_original;
    spriteset_ca25g_green;
}
switch (FEAT_TRAINS, SELF, switch_ca25g_graphics_subtype_0, param_gaozulv) {
    1: switch_ca25g_graphics_subtype_year_depend;
    spriteset_ca25g_original;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_graphics_subtype, cargo_subtype) {
    0: switch_ca25g_graphics_subtype_0;
    1: spriteset_ca25g_original;
    2: spriteset_ca25g_green;
    3: spriteset_ca25g_jinwen;
    4: spriteset_ca25g_jinwen_green;
    5: spriteset_ca25g_orange;
    6: spriteset_ca25g_gmsr;
    7: spriteset_ca25g_gmsr_early;
    8: spriteset_ca25g_hejiu;
    9: spriteset_ca25g_fulong;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_graphics, position_in_articulated_veh % 3) {
    1: switch_ca25g_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_capacity, position_in_articulated_veh % 3) {
    1: return 48;
    return 0;
}


item (FEAT_TRAINS, ca25g) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2048;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         44.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA25G), string(STR_CAFE_EFFECT), string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVALIABLE), string(STR_ORIGINAL), string(STR_GAOZU_GREEN), string(STR_JINWEN), string(STR_JINWEN_GREEN), string(STR_ORANGE), string(STR_GMSR), string(STR_GMSR_EARLY), string(STR_HEJIU), string(STR_FULONG)), string(STR_COMFORT_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca25g_cargo_subtype;

        // Graphics
        default:                        switch_ca25g_graphics;
        purchase:                       spriteset_ca25g_purchase_original;
        articulated_part:               switch_ca25g_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25g_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    25;
        running_cost_factor:            4 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   66;
        purchase_cargo_capacity:        48/3;
        name:                           switch_ca25g_name;

    }
}

// End CA25G

