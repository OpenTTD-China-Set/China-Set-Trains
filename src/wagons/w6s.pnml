// Begin W6S
switch (FEAT_TRAINS, SELF, switch_w6s_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BOXCAR);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_w6s_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "w6s")
}

// Vehicle
spriteset (spriteset_w6s, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "w6s")
}
spriteset (spriteset_w6s_white, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "w6s-white")
}

random_switch (FEAT_TRAINS, SELF, random_w6s) {
    1: spriteset_w6s; // More colours waiting to be added
    1: spriteset_w6s_white;
}

switch (FEAT_TRAINS, SELF, switch_w6s_articulated_part, extra_callback_info1) {
    1..2: return w6s;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_w6s_graphics, position_in_articulated_veh % 3) {
    1: random_w6s;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_w6s_capacity, position_in_articulated_veh % 3) {
    1: return 55;
    return 0;
}



item (FEAT_TRAINS, w6s) {
    property {
        // Menu
        name:                           string(STR_NAME_W6S);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3106;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2001,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       0;
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [SULP, ENSP, FMSP, FERT, NITR, NHNO, CHLO, NH3_, ACID, C2H4, SOAP, N7__, RFPR];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             FMSP;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         24 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_W_BOXCAR_LIVERY_AVAILABILITY), string(STR_FRESHNESS_HIGH, 400));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_w6s_graphics;
        purchase:                       spriteset_w6s_purchase;
        articulated_part:               switch_w6s_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 switch_w6s_capacity;
        cargo_age_period:               400 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    12;
        running_cost_factor:            10 * runningcostfactor();
        purchase_running_cost_factor:   40;
        purchase_cargo_capacity:        55 / 3;
        name:                           switch_w6s_name;
    }
}

// End W6S

