//Begin DF4BH
switch (FEAT_TRAINS, SELF, switch_df4bh_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_DF4_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_df4bh_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df4b")
}

spriteset (spriteset_df4bnb_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df4bnb")
}

//Adds Vehicle
spriteset (spriteset_df4bnb, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4bnb")
}
spriteset (spriteset_df4bnb_reverse, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4bnb-reverse")
}

switch (FEAT_TRAINS, SELF, switch_df4bh_articulated_part, extra_callback_info1) {
    1..2: return df4bh;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df4bh_graphics_subtype, cargo_subtype) {
    1: spriteset_df4b_orange;
    2: spriteset_df4_ukraine;
    3: spriteset_df4b_dprk;
    4: spriteset_df4b_jinwen;
    5: spriteset_df4b_shenhua;
    6: spriteset_df4b_guoneng;
    7: spriteset_df4bh_laos;
    8: spriteset_df4bh_wujing;
    9: spriteset_df4bnb;
    10: spriteset_df4bnb_reverse;
    spriteset_df4b_green;
}

switch (FEAT_TRAINS, SELF, switch_df4bh_cargo_subtype, cargo_subtype) {
    0: return string(STR_GREEN);
    1: return string(STR_ORANGE);
    2: return string(STR_UKRAINE);
    3: return string(STR_DF4B_DPRK);
    4: return string(STR_JINWEN);
    5: return string(STR_SHENHUA);
    6: return string(STR_GUONENG);
    7: return string(STR_CHN2LAOS);
    8: return string(STR_WUJING);
    9: return string(STR_DF4BN_ORIGINAL);
    10: return string(STR_DF4BN_REVERSE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df4bh_graphics, position_in_articulated_veh % 3) {
    1: switch_df4bh_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df4bh) {
    property {
        // Menu
        name:                           string(STR_NAME_DF4BH);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        variant_group:					5121;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1984,01,01);
        model_life:                     60;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          2610 hpM;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.305;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              14;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_DF4B), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF4_LIVERY_AVAILABILITY), string(STR_DF4B_NICKNAME), string(STR_RELIABILITY_14));
        cargo_subtype_text:             switch_df4bh_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df4bh_graphics;
        purchase:                       spriteset_df4bh_purchase;
        articulated_part:               switch_df4bh_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    73;
        running_cost_factor:            53 * runningcostfactor();
        purchase_running_cost_factor:   212;
        purchase_cargo_capacity:        0;
        name:                            switch_df4bh_name;

    }
}

// End DF4BH

