//Begin SRZXL25Z
switch (FEAT_TRAINS, SELF, switch_srzxl25z_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_S25Z_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_srzxl25z_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25z","srzxl25z")
}

// Vehicle
spriteset (spriteset_srzxl25z_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25z","srzxl25z")
}

switch (FEAT_TRAINS, SELF, switch_srzxl25z_articulated_part, extra_callback_info1) {
    1: return srzxl25z;
    2: return luggageroom_s25;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_srzxl25z_graphics, position_in_articulated_veh % 3) {
    1: spriteset_srzxl25z_original;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_srzxl25z_capacity, position_in_articulated_veh % 3) {
    1: return 66;
    return 0;
}


item (FEAT_TRAINS, srzxl25z) {
    property {
        // Menu
        name:                           string(STR_NAME_SRZXL25Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2104;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 92;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         49.2 ton;
        cost_factor:                    7;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            4;
    }

    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_SRZXL25Z), string(STR_FRESHNESS_MEDIUM, 256));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_srzxl25z_graphics;
        purchase:                       spriteset_srzxl25z_purchase_original;
        articulated_part:               switch_srzxl25z_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_srzxl25z_capacity;
        cargo_age_period:               (16 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    17;
        running_cost_factor:            11 * runningcostfactor();
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        66/3;
        name:                           switch_srzxl25z_name;

    }
}

// End SRZXL25Z
