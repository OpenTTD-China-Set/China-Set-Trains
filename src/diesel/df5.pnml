// Begin DF5

// Graphics

// Purchase Menu
spriteset (spriteset_df5_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df5")
}

// Vehicle
spriteset (spriteset_df5_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5")
}
spriteset (spriteset_df5_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5-red")
}
spriteset (spriteset_df5_yellow, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5-yellow")
}
spriteset (spriteset_df5_qinhuangdao, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5-qinhuangdao")
}
spriteset (spriteset_df5_prk, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5-prk")
}

switch (FEAT_TRAINS, SELF, switch_df5_articulated_part, extra_callback_info1) {
    1..2: return df5;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df5_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_RED);
    2: return string(STR_ORANGE);
    3: return string(STR_QINHUANGDAO);
    4: return string(STR_DF4B_DPRK);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df5_graphics_subtype, cargo_subtype) {
    0: spriteset_df5_original;
    1: spriteset_df5_red;
    2: spriteset_df5_yellow;
    3: spriteset_df5_qinhuangdao;
    4: spriteset_df5_prk;
}

switch (FEAT_TRAINS, SELF, switch_df5_graphics, position_in_articulated_veh % 3) {
    1: switch_df5_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df5_additional_text, param_cargo_decay) {
    return string(STR_DESC_3, string(STR_FULL_NAME_DF5), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_16));
}


item (FEAT_TRAINS, df5) {
    property {
        // Menu
        name:                           string(STR_NAME_DF5);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1984,01,01);
        model_life:                     60;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          80 km/h;
        power:                          1040 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.322;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                switch_df5_additional_text;
        cargo_subtype_text:             switch_df5_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df5_graphics;
        purchase:                       spriteset_df5_purchase;
        articulated_part:               switch_df5_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    46;
        running_cost_factor:            31 * runningcostfactor();
        purchase_running_cost_factor:   124;
        purchase_cargo_capacity:        0;
    }
}

// End DF5

