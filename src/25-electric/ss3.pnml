// Begin SS3

// Graphics

// Purchase Menu
spriteset (spriteset_ss3_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "ss3")
}

// Vehicle
spriteset (spriteset_ss3_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3")
}
spriteset (spriteset_ss3_blue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3-blue")
}
spriteset (spriteset_ss3_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3-red")
}
spriteset (spriteset_ss3_grassgreen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3-grassgreen")
}
spriteset (spriteset_ss3_fuzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3-fuzhou")
}
spriteset (spriteset_ss3_shenhua, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss3-shenhua")
}

switch (FEAT_TRAINS, SELF, switch_ss3_articulated_part, extra_callback_info1) {
    1..2: return ss3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ss3_graphics_subtype, cargo_subtype) {
    1: spriteset_ss3_shenhua;
    2: spriteset_ss3_blue;
    3: spriteset_ss3_red;
    4: spriteset_ss3_grassgreen;
    5: spriteset_ss3_fuzhou;
    spriteset_ss3_original;
}

switch (FEAT_TRAINS, SELF, switch_ss3_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_SHENHUA);
    2: return string(STR_BLUE);
    3: return string(STR_RED);
    4: return string(STR_GRASSGREEN);
    5: return string(STR_FUZHOU);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ss3_graphics, position_in_articulated_veh % 3) {
    1: switch_ss3_graphics_subtype;
    empty_sprites;
}

item (FEAT_TRAINS, ss3) {
    property {
        // Menu
        name:                           string(STR_NAME_SS3);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1978,01,01);
        model_life:                     60;
        retire_early:                   30;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          4350 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.347;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              10;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SS3), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_SS3_NICKNAME), string(STR_RELIABILITY_10));
        cargo_subtype_text:             switch_ss3_cargo_subtype;
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_ss3_graphics;
        purchase:                       spriteset_ss3_purchase_original;
        articulated_part:               switch_ss3_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    87;
        running_cost_factor:            71 * runningcostfactor();
        purchase_running_cost_factor:   284;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           0;
    }
}

// End SS3

