// Begin N60

// Graphics

// Purchase Menu
spriteset (spriteset_n60_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "n60")
}

// Vehicle
spriteset (spriteset_n60_original_empty, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "n60")
}
spriteset (spriteset_n60_original_cover, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "n60-cover")
}

switch (FEAT_TRAINS, SELF, switch_n60_original, cargo_count < 5) {
    0: spriteset_n60_original_cover;
    spriteset_n60_original_empty;
}

random_switch (FEAT_TRAINS, SELF, random_n60_colour) {
    1: switch_n60_original; // More colours waiting to be added
}

switch (FEAT_TRAINS, SELF, switch_n60_articulated_part, extra_callback_info1) {
    1..2: return n60;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_n60_graphics, position_in_articulated_veh % 3) {
    1: random_n60_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_n60_capacity_cargo, cargo_type_in_veh) {
    GOOD: return 90;
    return 60;
}

switch (FEAT_TRAINS, SELF, switch_n60_capacity, position_in_articulated_veh % 3) {
    1: switch_n60_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_n60_loading_speed, cargo_type_in_veh) {
    GOOD: return 15;
    return 10;
}


item (FEAT_TRAINS, n60) {
    property {
        // Menu
        name:                           string(STR_NAME_N60);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1958,01,01);
        model_life:                     60;
        retire_early:                   15;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_EXPRESS, CC_BULK, CC_PIECE_GOODS, CC_COVERED, CC_OVERSIZED);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_LIQUID, CC_HAZARDOUS);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [YETI, YETY];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  10 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          90 km/h;
        power:                          0 hp;
        weight:                         18 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_N60_LIVERY_AVAILABILITY), string(STR_DECAY_185));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_n60_graphics;
        purchase:                       spriteset_n60_purchase;
        articulated_part:               switch_n60_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_n60_capacity;
        loading_speed:                  switch_n60_loading_speed() << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;
        cost_factor:                    4;
        running_cost_factor:            3 * runningcostfactor();
        purchase_running_cost_factor:   12;
        purchase_cargo_capacity:        60/3;
    }
}

// End N60

