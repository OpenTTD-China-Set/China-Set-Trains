// Begin DF

// Graphics

spriteset (spriteset_df_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df-b")
}

spriteset (spriteset_df_standard, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df-b")
}

spriteset (spriteset_df_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df")
}

spriteset (spriteset_df_whiteline_standard, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df-whiteline-b")
}

spriteset (spriteset_df_whiteline_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df-whiteline")
}

switch (FEAT_TRAINS, SELF, switch_df_articulated_part, extra_callback_info1) {
    1..2: return df;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df_graphics_subtype_default, cargo_subtype) {
    1: spriteset_df_whiteline_standard;
    spriteset_df_standard;
}

switch (FEAT_TRAINS, SELF, switch_df_graphics_subtype_reversed, cargo_subtype) {
    1: spriteset_df_whiteline_reversed;
    spriteset_df_reversed;
}

switch (FEAT_TRAINS, SELF, switch_df_cargo_subtype, cargo_subtype) {
    0: return string(STR_DF_YELLOW);
    1: return string(STR_DF_WHITE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df_direction, (position_in_consist != 1) && (position_in_consist_from_end == 1) || ((position_in_vehid_chain % 6) == 4)) {
    0: switch_df_graphics_subtype_default;
    switch_df_graphics_subtype_reversed;
}

switch (FEAT_TRAINS, SELF, switch_df_graphics, position_in_articulated_veh % 3) {
    1: switch_df_direction;
    empty_sprites;
}


item (FEAT_TRAINS, df) {
    property {
        // Menu
        name:                           string(STR_NAME_DF);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1958,09,26);
        model_life:                     40;
        retire_early:                   30;
        vehicle_life:                   40;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          1800 hpM;
        weight:                         126 ton;
        tractive_effort_coefficient:    0.245;
        cost_factor:                    45;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            35;
        reliability_decay:              16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_6, string(STR_FULL_NAME_DF),  string(STR_ALIASNAME_DF), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF_LIVERY_AVAILABILITY), string(STR_DF_NICKNAME), string(STR_RELDEC_MEDIUM, 16));
        cargo_subtype_text:             switch_df_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df_graphics;
        purchase:                       spriteset_df_purchase;
        articulated_part:               switch_df_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    45;
        running_cost_factor:            35 * runningcostfactor();
        purchase_running_cost_factor:   140;
        purchase_cargo_capacity:        0;
    }
}

// End DF

