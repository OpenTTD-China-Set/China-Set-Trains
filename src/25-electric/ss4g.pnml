//Begin SS4G
switch (FEAT_TRAINS, SELF, switch_ss4g_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_SS4_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_ss4g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "ss4g-front")
}

// Vehicle
spriteset (spriteset_ss4g_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4g-front")
}
spriteset (spriteset_ss4g_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4g-back")
}
spriteset (spriteset_ss4g_alternative_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4g-alternative-front")
}
spriteset (spriteset_ss4g_alternative_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4g-alternative-back")
}

switch (FEAT_TRAINS, SELF, switch_ss4g_articulated_part, extra_callback_info1) {
    1..5: return ss4g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ss4g_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_ALTERNATIVE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ss4g_graphics_subtype_0, position_in_articulated_veh % 6) {
    1: spriteset_ss4g_front;
    4: spriteset_ss4g_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ss4g_graphics_subtype_1, position_in_articulated_veh % 6) {
    1: spriteset_ss4g_alternative_front;
    4: spriteset_ss4g_alternative_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ss4g_graphics_subtype, cargo_subtype) {
    0: switch_ss4g_graphics_subtype_0;
    1: switch_ss4g_graphics_subtype_1;
}

switch (FEAT_TRAINS, SELF, switch_ss4g_graphics, position_in_articulated_veh % 3) {
    1: switch_ss4g_graphics_subtype;
    empty_sprites;
}

item (FEAT_TRAINS, ss4g) {
    property {
        // Menu
        name:                           string(STR_NAME_SS4G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					6150;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          6400 kW;
        weight:                         184 ton;
        tractive_effort_coefficient:    0.348;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              8;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SS4G), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_SS4G_NICKNAME), string(STR_RELIABILITY_8));
        cargo_subtype_text:             switch_ss4g_cargo_subtype;
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_ss4g_graphics;
        purchase:                       spriteset_ss4g_purchase_original;
        articulated_part:               switch_ss4g_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    110;
        running_cost_factor:            89 * runningcostfactor();
        purchase_running_cost_factor:   356;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           0;
        name:                            switch_ss4g_name;

    }
}

// End SS4G

