//Begin GN70
switch (FEAT_TRAINS, SELF, switch_gn70_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_TANKER);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_gn70_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "gn70")
}

// Vehicle
spriteset (spriteset_gn70_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "gn70")
}
spriteset (spriteset_gn70_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "gn70-red")
}

random_switch (FEAT_TRAINS, SELF, random_gn70_colour) {
    1: spriteset_gn70_black;
    1: spriteset_gn70_red;
}
switch (FEAT_TRAINS, SELF, switch_gn70_cargo_subtype, cargo_subtype) {
    0: return string(STR_RANDOM_COLOR);
    1: return string(STR_BLACK);
    2: return string(STR_RED);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_gn70_graphics_subtype, cargo_subtype) {
    0: random_gn70_colour;
    1: spriteset_gn70_black;
    2: spriteset_gn70_red;
}

switch (FEAT_TRAINS, SELF, switch_gn70_articulated_part, extra_callback_info1) {
    1..2: return gn70;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_gn70_graphics, position_in_articulated_veh % 3) {
    1: switch_gn70_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_gn70_capacity, position_in_articulated_veh % 3) {
    1: return 70;
    return 0;
}


item (FEAT_TRAINS, gn70) {
    property {
        // Menu
        name:                           string(STR_NAME_GN70);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3088;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2005,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_LIQUID, CC_HAZARDOUS);
        non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 70;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         23.4 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_GN70_LIVERY_AVAILABILITY), string(STR_FRESHNESS_MEDIUM, 185));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_gn70_cargo_subtype;

        // Graphics
        default:                        switch_gn70_graphics;
        purchase:                       spriteset_gn70_purchase;
        articulated_part:               switch_gn70_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_gn70_capacity;
        cargo_age_period:               185 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    6;
        running_cost_factor:            5 * runningcostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        70/3;
        name:                            switch_gn70_name;

    }
}

// End GN70

