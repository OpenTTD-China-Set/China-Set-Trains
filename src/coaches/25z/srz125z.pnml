switch (FEAT_TRAINS, SELF, switch_srz125z_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_S25Z_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_srz125z_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "srz125z")
}

// Vehicle
spriteset (spriteset_srz125z_beijing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches","srz125z")
}
spriteset (spriteset_srz125z_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "srz125z-guangzhou")
}

switch (FEAT_TRAINS, SELF, switch_srz125z_articulated_part, extra_callback_info1) {
    1..2: return srz125z;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_srz125z_cargo_subtype, cargo_subtype) {
    0: return string(STR_BEIJING);
    1: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_srz125z_graphics_subtype, cargo_subtype) {
    0: spriteset_srz125z_beijing;
    1: spriteset_srz125z_guangzhou;
}

switch (FEAT_TRAINS, SELF, switch_srz125z_graphics, position_in_articulated_veh % 3) {
    1: switch_srz125z_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_srz125z_capacity, position_in_articulated_veh % 3) {
    1: return 108;
    return 0;
}


item (FEAT_TRAINS, srz125z) {
    property {
        // Menu
        name:                           string(STR_NAME_SRZ125Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2104;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 96;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               222 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         53 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }

    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_SRZ125Z), string(STR_S25Z_LIVERY_AVAILABILITY), string(STR_COMFORT_256));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_srz125z_cargo_subtype;

        // Graphics
        default:                        switch_srz125z_graphics;
        purchase:                       spriteset_srz125z_purchase_original;
        articulated_part:               switch_srz125z_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_srz125z_capacity;
        cargo_age_period:               (16 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    12;
        running_cost_factor:            11 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        108/3;
        name:                            switch_srz125z_name;

    }
}

