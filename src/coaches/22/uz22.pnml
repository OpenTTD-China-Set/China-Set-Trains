//Begin UZ22
switch (FEAT_TRAINS, SELF, switch_uz22_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_22_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_uz22_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "uz22")
}

// Vehicle
spriteset (spriteset_uz22_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "uz22")
}

spriteset (spriteset_uz22_beijing2shanghai, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "uz22-beijing2shanghai")
}
spriteset (spriteset_uz22_beijing2shanghai_alternative, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "uz22-beijing2shanghai-alternative")
}
spriteset (spriteset_uz22_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "uz22-guangzhou")
}

switch (FEAT_TRAINS, SELF, switch_uz22_articulated_part, extra_callback_info1) {
    1..2: return uz22;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_uz22_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_BEIJING2SHANGHAI);
    2: return string(STR_BEIJING2SHANGHAIALT);
    3: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_uz22_graphics_subtype, cargo_subtype) {
    0: spriteset_uz22_original;
    1: spriteset_uz22_beijing2shanghai;
    2: spriteset_uz22_beijing2shanghai_alternative;
    3: spriteset_uz22_guangzhou;
}

switch (FEAT_TRAINS, SELF, switch_uz22_graphics, position_in_articulated_veh % 3) {
    1: switch_uz22_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_uz22_capacity, position_in_articulated_veh % 3) {
    1: return 68;
    return 0;
}


item (FEAT_TRAINS, uz22) {
    property {
        // Menu
        name:                           string(STR_NAME_UZ22);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					yz22;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1959,01,01);
        model_life:                     35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [MAIL, VALU, GOLD, DIAM, GOOD];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             MAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 68;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         43 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_UZ22), string(STR_22_LIVERY_AVAILABILITY), string(STR_DECAY_200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_uz22_cargo_subtype;

        // Graphics
        default:                        switch_uz22_graphics;
        purchase:                       spriteset_uz22_purchase_original;
        articulated_part:               switch_uz22_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 switch_uz22_capacity;
        cargo_age_period:               200 << param_cargo_decay;
        cost_factor:                    7;
        running_cost_factor:            5 * runningcostfactor() * cafecostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        68/3;
        name:                            switch_uz22_name;

    }
}

// End UZ22

