// Begin HXD3C
switch (FEAT_TRAINS, SELF, switch_hxd3c_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_HXD3_SERIES);
    return CB_RESULT_NO_TEXT;
}

// Graphics

// Purchase Menu
spriteset (spriteset_hxd3c_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "hxd3c")
}

// Vehicle
spriteset (spriteset_hxd3c_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "hxd3c")
}

switch (FEAT_TRAINS, SELF, switch_hxd3c_articulated_part, extra_callback_info1) {
    1..2: return hxd3c;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxd3c_graphics, position_in_articulated_veh % 3) {
    1: spriteset_hxd3c_original;
    empty_sprites;
}


item (FEAT_TRAINS, hxd3c) {
    property {
        // Menu
        name:                           string(STR_NAME_HXD3C);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					hxd3;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2010,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          7200 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.3845;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_HXD3C), string(STR_ELECTRICITY_SUPPLY_YES), string(STR_HXD3C_NICKNAME), string(STR_RELDEC_VERY_LOW, 6));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_hxd3c_graphics;
        purchase:                       spriteset_hxd3c_purchase_original;
        articulated_part:               switch_hxd3c_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    117;
        running_cost_factor:            95 * runningcostfactor() + 20;
        purchase_running_cost_factor:   400;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           airconditioner;
        name:                           switch_hxd3c_name;
    }
}

// End HXD3C

