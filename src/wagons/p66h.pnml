//Begin P66H
switch (FEAT_TRAINS, SELF, switch_p66h_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BOXCAR);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_p66h_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p66h")
}

// Vehicle
spriteset (spriteset_p66h_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p66h")
}
spriteset (spriteset_p66h_alternative, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p66h-alternative")
}

random_switch (FEAT_TRAINS, SELF, random_p66h_colour) {
    1: spriteset_p66h_original; // More colours waiting to be added
    1: spriteset_p66h_alternative;
}
switch (FEAT_TRAINS, SELF, switch_p66h_cargo_subtype, cargo_subtype) {
    0: return string(STR_RANDOM_COLOR);
    1: return string(STR_ORIGINAL);
    2: return string(STR_ALTERNATIVE);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_p66h_graphics_subtype, cargo_subtype) {
    0: random_p66h_colour;
    1: spriteset_p66h_original;
    2: spriteset_p66h_alternative;
}

switch (FEAT_TRAINS, SELF, switch_p66h_articulated_part, extra_callback_info1) {
    1..2: return p66h;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p66h_graphics, position_in_articulated_veh % 3) {
    1: switch_p66h_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_p66h_capacity_cargo, cargo_type_in_veh) {
    MAIL: return 160;
    GOOD: return 120;
    return 80;
}

switch (FEAT_TRAINS, SELF, switch_p66h_capacity, position_in_articulated_veh % 3) {
    1: switch_p66h_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p66h_loading_speed, cargo_type_in_veh) {
    MAIL: return 10;
    GOOD: return 8;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p66h_decay, cargo_type_in_veh) {
    LVST: return 560;
    return 185;
}


item (FEAT_TRAINS, p66h) {
    property {
        // Menu
        name:                           string(STR_NAME_P66H);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					p1;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2004,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SADN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED, CC_EXPRESS, CC_PIECE_GOODS, CC_COVERED);
        non_refittable_cargo_classes:   bitmask(CC_LIQUID, CC_HAZARDOUS, CC_OVERSIZED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [YETI, YETY];
        refit_cost:                     0;
        default_cargo_type:             GOOD;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  24 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         23.8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_DECAY_BOXCAR_NOPASS));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_p66h_cargo_subtype;

        // Graphics
        default:                        switch_p66h_graphics;
        purchase:                       spriteset_p66h_purchase_original;
        articulated_part:               switch_p66h_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 switch_p66h_capacity;
        loading_speed:                  switch_p66h_loading_speed() << param_loading_speed;
        cargo_age_period:               switch_p66h_decay() << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    5;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        60/3;
        name:                            switch_p66h_name;

    }
}

// End P66H
