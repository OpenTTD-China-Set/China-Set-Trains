// Begin CA25C

// Graphics

// Purchase Menu
spriteset (spriteset_ca25c_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25c", "ca25c")
}

// Vehicle
spriteset (spriteset_ca25c, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25c", "ca25c")
}

switch (FEAT_TRAINS, SELF, switch_ca25c_articulated_part, extra_callback_info1) {
    1..2: return ca25c;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}




switch (FEAT_TRAINS, SELF, switch_ca25c_graphics, position_in_articulated_veh % 3) {
    1: spriteset_ca25c;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca25c_capacity, position_in_articulated_veh % 3) {
    1: return 72;
    return 0;
}


item (FEAT_TRAINS, ca25c) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25C);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					rz225c;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1994,01,01);
        model_life:                     20;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         52.5 ton;
        cost_factor:                    1;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            1;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA25C), string(STR_CAFE_EFFECT), string(STR_AIR_CONDITIONER_NEED_POWER), string(STR_COMFORT_MEDIUM, 192));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_ca25c_graphics;
        purchase:                       spriteset_ca25c_purchase;
        articulated_part:               switch_ca25c_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25c_capacity;
        cargo_age_period:               (12 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    8;
        running_cost_factor:            5 * runningcostfactor() * cafecostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   70;
        purchase_cargo_capacity:        36/3;
        name:                           switch_25c_name;
    }
}

// End CA25C