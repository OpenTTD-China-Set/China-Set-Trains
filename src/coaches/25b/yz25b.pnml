// Begin YZ25B
switch (FEAT_TRAINS, SELF, switch_yz25b_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25B_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_yz25b_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25b/yz25b")
}

// Vehicle
spriteset (spriteset_yz25b_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/yz25b")
}
spriteset (spriteset_yz25b_xiyan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/yz25b-xiyan")
}
spriteset (spriteset_25b_jitong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/25b-jitong")
}

switch (FEAT_TRAINS, SELF, switch_yz25b_articulated_part, extra_callback_info1) {
    1..2: return yz25b;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORDINARY_GREEN);
/*     2: return string(STR_GAOZU_GREEN); */
    2: return string(STR_XIYAN);
    3: return string(STR_JITONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_graphics_build_year_depend, build_year) {
    0..2014: spriteset_yz25b_original; // Unfinished
    spriteset_yz25b_original;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_cargo_subtype_0, param_gaozulv) {
    1: switch_yz25b_graphics_build_year_depend;
    spriteset_yz25b_original;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_graphics_subtype, cargo_subtype) {
    0: switch_yz25b_cargo_subtype_0;
    1: spriteset_yz25b_original;
    /* 2: ; */
    2: spriteset_yz25b_xiyan;
    3: spriteset_25b_jitong;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_graphics, position_in_articulated_veh % 3) {
    1: switch_yz25b_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_yz25b_capacity, position_in_articulated_veh % 3) {
    1: return 128;
    return 0;
}


item (FEAT_TRAINS, yz25b) {
    property {
        // Menu
        name:                           string(STR_NAME_YZ25B);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         44.7 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_YZ25B), string(STR_LV_AVAILABLE_S ,string(STR_LIVERY_AVALIABLE), string(STR_ORIGINAL)/* , string(STR_GAOZU_GREEN) */, string(STR_XIYAN), string(STR_JITONG)), string(STR_COMFORT_LOW, 144), string(STR_NO_AIR_CONDITIONER));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_yz25b_graphics;
        purchase:                       spriteset_yz25b_purchase_original;
        articulated_part:               switch_yz25b_articulated_part;
        cargo_subtype_text:             switch_yz25b_cargo_subtype;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_yz25b_capacity;
        cargo_age_period:               (9 * 4 * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    7;
        running_cost_factor:            4 * runningcostfactor() * cafecostfactor(); // NO AC FUNCTION
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        128/3;
        name:                           switch_yz25b_name;
    }
}

// End YZ25B

