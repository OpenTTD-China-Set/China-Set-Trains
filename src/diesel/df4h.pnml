// Begin DF4H

// Graphics

switch (FEAT_TRAINS, SELF, switch_df4h_articulated_part, extra_callback_info1) {
    1..2: return df4h;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df4h_graphics_subtype, cargo_subtype) {
    1: spriteset_df4_orange;
    2: spriteset_df4b_green;
    3: spriteset_df4_ukraine;
    4: spriteset_df4_jtzc;
    5: spriteset_df4_nenhei_railway;
    spriteset_df4_green;
}

switch (FEAT_TRAINS, SELF, switch_df4h_cargo_subtype, cargo_subtype) {
    0: return string(STR_GREEN);
    1: return string(STR_ORANGE);
    2: return string(STR_LATER_GREEN);
    3: return string(STR_UKRAINE);
    4: return string(STR_DF4_JTZC);
    5: return string(STR_NENHEI);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df4h_graphics, position_in_articulated_veh % 3) {
    1: switch_df4h_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df4h) {
    property {
        // Menu
        name:                           string(STR_NAME_DF4H);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1969,01,01);
        model_life:                     60;
        retire_early:                   40;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          2610 hpM;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.305;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              32;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_DF4), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF4_LIVERY_AVAILABILITY), string(STR_DF4_NICKNAME), string(STR_RELIABILITY_32));
        cargo_subtype_text:             switch_df4h_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df4h_graphics;
        purchase:                       spriteset_df4_purchase_green;
        articulated_part:               switch_df4h_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    54;
        running_cost_factor:            51 * runningcostfactor();
        purchase_running_cost_factor:   204;
        purchase_cargo_capacity:        0;
    }
}

// End DF4H

