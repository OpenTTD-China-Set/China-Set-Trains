//Begin TZ2
switch (FEAT_TRAINS, SELF, switch_tz2_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_tz2_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25", "tz2-sfk50")
}

// Vehicle
spriteset (spriteset_tz2_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25", "tz2-sfk50")
}
spriteset (spriteset_tz2_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25", "tz2-sfk50-red")
}
spriteset (spriteset_tz2_beidaihe, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25", "kd25-beidaihe")
}

switch (FEAT_TRAINS, SELF, switch_tz2_articulated_part, extra_callback_info1) {
    1..2: return tz2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_tz2_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    2: string(STR_RED);
    3: return string(STR_BEIDAIHE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_tz2_graphics_subtype_0, build_year) {
    0..2002: spriteset_tz2_original;
    spriteset_tz2_red;
}

switch (FEAT_TRAINS, SELF, switch_tz2_graphics_subtype, cargo_subtype) {
    0: switch_tz2_graphics_subtype_0;
    1: spriteset_tz2_original;
    2: spriteset_tz2_red;
    3: spriteset_tz2_beidaihe;
}

switch (FEAT_TRAINS, SELF, switch_tz2_graphics, position_in_articulated_veh % 3) {
    1: switch_tz2_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, tz2) {
    property {
        // Menu
        name:                           string(STR_NAME_TZ2);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2125;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1981,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         62 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_TZ2), string(STR_25ML_LIVERY_AVAILABILITY), string(STR_AIR_CONDITIONER_EFFECT));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_tz2_graphics;
        purchase:                       spriteset_tz2_purchase_original;
        cargo_subtype_text:             switch_tz2_cargo_subtype;
        articulated_part:               switch_tz2_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cargo_age_period:               200 << param_cargo_decay;
        cost_factor:                    23;
        running_cost_factor:            6 * runningcostfactor() + 20;
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        0;
        name:                            switch_tz2_name;

    }
}

// End TZ2

