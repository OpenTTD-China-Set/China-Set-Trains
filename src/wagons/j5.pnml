// Begin J5

// Graphics

// Purchase Menu
spriteset (spriteset_j5_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "j5")
}

// Vehicle
spriteset (spriteset_j5, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "j5")
}
spriteset (spriteset_j5_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "j5-full")
}
spriteset (spriteset_j5_grey, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "j5-grey")
}
spriteset (spriteset_j5_grey_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "j5-grey-full")
}

switch (FEAT_TRAINS, SELF, switch_j5_articulated_part, extra_callback_info1) {
    1..2: return j5;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_j5_white, cargo_count < 5) {
    0: spriteset_j5_full;
    spriteset_j5;
}

switch (FEAT_TRAINS, SELF, switch_j5_grey, cargo_count < 5) {
    0: spriteset_j5_grey_full;
    spriteset_j5_grey;
}

random_switch (FEAT_TRAINS, SELF, random_j5_colour) {
    1: switch_j5_white;
    1: switch_j5_grey;
}

switch (FEAT_TRAINS, SELF, switch_j5_graphics, position_in_articulated_veh % 3) {
    1: random_j5_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_j5_capacity_cargo, cargo_type_in_veh) {
    LVST: return 20;
}

switch (FEAT_TRAINS, SELF, switch_j5_capacity, position_in_articulated_veh % 3) {
    1: switch_j5_capacity_cargo;
    return 0;
}



item (FEAT_TRAINS, j5) {
    property {
        // Menu
        name:                           string(STR_NAME_J5);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1984,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       0;
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [LVST];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             LVST;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 20;
        loading_speed:                  3 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         27 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_FRESHNESS_VERY_HIGH, 800);
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_j5_graphics;
        purchase:                       spriteset_j5_purchase;
        articulated_part:               switch_j5_articulated_part;

        // Stats
        length:                         switch_length_2_5_2;
        cargo_capacity:                 switch_j5_capacity;
        cargo_age_period:               800 << param_cargo_decay;
        cost_factor:                    19;
        running_cost_factor:            17 * runningcostfactor();
        purchase_running_cost_factor:   68;
        purchase_cargo_capacity:        switch_j5_capacity_cargo() / 3;
    }
}

// End J5

