//Begin RW25T
switch (FEAT_TRAINS, SELF, switch_rw25t_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25T_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
// // Purchase Menu
spriteset (spriteset_rw25t_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25t/rw25t")
}

// Vehicle
spriteset (spriteset_rw25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/rw25t")
}
spriteset (spriteset_rw25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/rw25t-green")
}
spriteset (spriteset_bsprw25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bsprw25t")
}
spriteset (spriteset_bsprw25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bsprw25t-green")
}
spriteset (spriteset_rw25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/rw25t-xz")
}
spriteset (spriteset_bsprw25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bsprw25t-xz")
}
spriteset (spriteset_rw25t_int_dprk, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/rw25t-int-dprk")
}

switch (FEAT_TRAINS, SELF, switch_rw25t_articulated_part, extra_callback_info1) {
    1..2: return rw25t;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_rw25t_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    2: string(STR_BSP25T);
    3: string(STR_GAOZU_GREEN);
    4: string(STR_GAOZU_GREEN_BSP);
    5: string(STR_QINGZANG);
    6: string(STR_QINGZANG_BSP);
    7: string(STR_INTERNATIONAL_DPRK);
    return CB_RESULT_NO_TEXT;
}


switch (FEAT_TRAINS, SELF, switch_rw25t_graphics_subtype_year_depend, build_year) {
     0..2014: spriteset_rw25t_original;
    spriteset_rw25t_green;
}
switch (FEAT_TRAINS, SELF, switch_rw25t_graphics_subtype_0, param_gaozulv) {
    1: switch_rw25t_graphics_subtype_year_depend;
    spriteset_rw25t_original;
}



switch (FEAT_TRAINS, SELF, switch_rw25t_graphics_subtype, cargo_subtype) {
    0: switch_rw25t_graphics_subtype_0;
    1: spriteset_rw25t_original;
    2: spriteset_bsprw25t_original;
    3: spriteset_rw25t_green;
    4: spriteset_bsprw25t_green;
    5: spriteset_rw25t_qingzang;
    6: spriteset_bsprw25t_qingzang;
    7: spriteset_rw25t_int_dprk;
}

switch (FEAT_TRAINS, SELF, switch_rw25t_graphics, position_in_articulated_veh % 3) {
    1: switch_rw25t_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_rw25t_capacity, position_in_articulated_veh % 3) {
    1: return 36;
    return 0;
}


item (FEAT_TRAINS, rw25t) {
    property {
        // Menu
        name:                           string(STR_NAME_RW25T);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2052;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2003,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 36;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               444 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          0 hp;
        weight:                         51.3 ton;
        cost_factor:                    14;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            11;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_RW25T), string(STR_RW25T_LIVERY_AVAILABILITY), string(STR_COMFORT_HIGH, 384));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_rw25t_cargo_subtype;

        // Graphics
        default:                        switch_rw25t_graphics;
        purchase:                       spriteset_rw25t_purchase_original;
        articulated_part:               switch_rw25t_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_rw25t_capacity;
        cargo_age_period:               (24 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    14;
        running_cost_factor:            15 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   60;
        purchase_cargo_capacity:        36/3;
        name:                           switch_rw25t_name;

    }
}

// End RW25T

