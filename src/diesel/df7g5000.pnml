// Begin DF7G5000

// Graphics

// Purchase Menu
spriteset (spriteset_df7g5000_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df7g-5000")
}

// Vehicle
spriteset (spriteset_df7g5000_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df7g-5000")
}



switch (FEAT_TRAINS, SELF, switch_df7g5000_articulated_part, extra_callback_info1) {
    1..2: return df7g5000;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df7g5000_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);

    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df7g5000_graphics_subtype, cargo_subtype) {
    0: spriteset_df7g5000_original;

}

switch (FEAT_TRAINS, SELF, switch_df7g5000_graphics, position_in_articulated_veh % 3) {
    1: switch_df7g5000_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df7g5000) {
    property {
        // Menu
        name:                           string(STR_NAME_DF7G5000);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        variant_group:					5131;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2003,12,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   40;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          1500 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.322;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_DF7G), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF7G5000_LIVERY_AVAILABILITY), string(STR_DF7G_NICKNAME), string(STR_RELIABILITY_16));
        cargo_subtype_text:             switch_df7g5000_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df7g5000_graphics;
        purchase:                       spriteset_df7g5000_purchase;
        articulated_part:               switch_df7g5000_articulated_part;

        // Stats
        length:                         switch_length_2_5_2;
        cargo_capacity:                 0;
        cost_factor:                    49;
        running_cost_factor:            38 * runningcostfactor();
        purchase_running_cost_factor:   152;
        purchase_cargo_capacity:        0;
    }
}

// End DF7G5000

