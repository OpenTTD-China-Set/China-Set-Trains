// Begin CRH6A (3-Door)

// Graphics

// Purchase Menu
spriteset (spriteset_crh6a3_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_crh6a3_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "crh6a-c-jingtong-1")
}

// Vehicle
spriteset (spriteset_crh6a3_front_jingtong) {
    template_empty ()
}
spriteset (spriteset_crh6a3_middle_m_jingtong) {
    template_empty ()
}
spriteset (spriteset_crh6a3_middle_p_jingtong) {
    template_empty ()
}
spriteset (spriteset_crh6a3_cab_jingtong) {
    template_empty ()
}

alternative_sprites (spriteset_crh6a3_front_jingtong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-c-jingtong-1")
}
alternative_sprites (spriteset_crh6a3_middle_m_jingtong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-m-3-jingtong")
}
alternative_sprites (spriteset_crh6a3_middle_p_jingtong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-p-3-jingtong")
}
alternative_sprites (spriteset_crh6a3_cab_jingtong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh6a-c-jingtong-2")
}


spriteset (spriteset_crh6a3_front_jinshan) {
    template_empty ()
}
spriteset (spriteset_crh6a3_middle_m_jinshan) {
    template_empty ()
}
spriteset (spriteset_crh6a3_middle_p_jinshan) {
    template_empty ()
}
spriteset (spriteset_crh6a3_cab_jinshan) {
    template_empty ()
}

alternative_sprites (spriteset_crh6a3_front_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-c-jinshan-1")
}
alternative_sprites (spriteset_crh6a3_middle_m_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-m-3-jinshan")
}
alternative_sprites (spriteset_crh6a3_middle_p_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6a-p-3-jinshan")
}
alternative_sprites (spriteset_crh6a3_cab_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh6a-c-jinshan-2")
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_articulated_part, extra_callback_info1) {
    1..2: return crh6a3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_cargo_subtype, cargo_subtype) {
    0: return string(STR_JINGTONG);
    1: return string(STR_JINSHAN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_front, cargo_subtype) {
    0: spriteset_crh6a3_front_jingtong;
    1: spriteset_crh6a3_front_jinshan;
}

switch (FEAT_TRAINS, PARENT, switch_crh6a3_middle_p, cargo_subtype) {
    0: spriteset_crh6a3_middle_p_jingtong;
    1: spriteset_crh6a3_middle_p_jinshan;
}

switch (FEAT_TRAINS, PARENT, switch_crh6a3_middle_m, cargo_subtype) {
    0: spriteset_crh6a3_middle_m_jingtong;
    1: spriteset_crh6a3_middle_m_jinshan;
}

switch (FEAT_TRAINS, PARENT, switch_crh6a3_cab, cargo_subtype) {
    0: spriteset_crh6a3_cab_jingtong;
    1: spriteset_crh6a3_cab_jinshan;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6a3_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_head_capacity, position_in_articulated_veh % 3) {
    1: return 150;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_additional_text, param_cargo_decay) {
    return string(STR_DESC_4, string(STR_CRH6A3_CAN_ATTACH_WAGON), string(STR_CRH6A3_LIVERY_AVAILABILITY), string(STR_RELIABILITY_8), string(STR_CRH6A3_CONSIST), string(STR_CRH6A3_HEAD_SEAT));
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_short_graphics, (position_in_consist_from_end == position_in_consist) || (position_in_consist_from_end == position_in_consist + 3)) {
    1: switch_crh6a3_middle_p;
    switch_crh6a3_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_long_graphics, (position_in_consist_from_end == position_in_consist + 12) || (position_in_consist_from_end == position_in_consist + 15) || (position_in_consist_from_end + 12 == position_in_consist) || (position_in_consist_from_end + 15 == position_in_consist)) {
    1: switch_crh6a3_middle_p;
    switch_crh6a3_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_middle_graphics_1, num_vehs_in_consist) {
    0..24: switch_crh6a3_short_graphics;
    switch_crh6a3_long_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_middle_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6a3_middle_graphics_1;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_cab_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6a3_cab;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_graphics, position_in_consist_from_end) {
    0..2: switch_crh6a3_cab_graphics;
    switch_crh6a3_middle_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_length, position_in_consist_from_end) {
    switch_length_2_8_2;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_middle_capacity, position_in_articulated_veh % 3) {
    1: return 201;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_cab_capacity, position_in_articulated_veh % 3) {
    1: return 150;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_capacity, position_in_consist_from_end) {
    0..2: switch_crh6a3_cab_capacity;
    switch_crh6a3_middle_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_cost, vehicle_is_potentially_powered) {
    1: return 62;
    return 23;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_running, vehicle_is_potentially_powered) {
    1: return 48;
    return 9;
}

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_power, position_in_consist % 24) {
    0..2: return 0;
    3..8: return 1482;
    9..11: return 0;
    12..14: return 1482;
    15..17: return 0;
    18..20: return 1482;
    21..23: return 0;
    return 0;
} // T-M-M-T-M-T-M-T (IRL: TMMTTMMT, but compromise for gameplay)

switch (FEAT_TRAINS, SELF, switch_crh6a3_wagon_traction, vehicle_is_potentially_powered) {
    1: return int(0.16*255);
    return 0;
}


item (FEAT_TRAINS, crh6a3) {
    property {
        // Menu
        name:                           string(STR_NAME_CRH6A3);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2017,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SABA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  18 << param_loading_speed;
        cargo_age_period:               160 << param_cargo_decay;

        // Stats
        speed:                          200 km/h;
        power:                          1090 kW;
        weight:                         48 ton;
        tractive_effort_coefficient:    0.16;
        cost_factor:                    61;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            26;
        reliability_decay:              8;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                switch_crh6a3_additional_text;
        can_attach_wagon:               onlyze;
        start_stop:                     mu4car;
        cargo_subtype_text:             switch_crh6a3_cargo_subtype;

        // Graphics
        default:                        switch_crh6a3_graphics;
        purchase:                       spriteset_crh6a3_purchase_original;
        articulated_part:               switch_crh6a3_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        power:                          return 0;
        purchase_power:                 return 1482;
        tractive_effort_coefficient:    return 0;
        purchase_tractive_effort_coefficient: return int(0.16*255);
        cargo_capacity:                 switch_crh6a3_head_capacity;
        cargo_age_period:               160 << param_cargo_decay;
        cost_factor:                    61;
        running_cost_factor:            47 * runningcostfactor() + 20;
        purchase_running_cost_factor:   208;
        purchase_cargo_capacity:        150/3;
    }

    // Multiple Unit Wagons
    livery_override(zemuw) {
        default:                        switch_crh6a3_wagon_graphics;
        cargo_capacity:                 switch_crh6a3_wagon_capacity;
        power:                          switch_crh6a3_wagon_power;
        tractive_effort_coefficient:    switch_crh6a3_wagon_traction;
        length:                         switch_crh6a3_wagon_length;
        weight:                         48;
        can_attach_wagon:               muhead;
        cargo_age_period:               (160) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh6a3_wagon_cost;
        running_cost_factor:            switch_crh6a3_wagon_running() * runningcostfactor() + accostfactor();
    }
}

// End CRH6A (3-Door)

