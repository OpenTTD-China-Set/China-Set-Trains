//Begin XL25ML
switch (FEAT_TRAINS, SELF, switch_xl25ml_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_xl25ml_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "xl25ml-green")
}

// Vehicle
spriteset (spriteset_xl25ml_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "xl25ml-green")
}
spriteset (spriteset_xl25ml_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "xl25ml-red")
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_articulated_part, extra_callback_info1) {
    1..2: return xl25ml;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_graphics_subtype_0, build_year) {
    0..2002: spriteset_xl25ml_original;
    spriteset_xl25ml_red;
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_graphics_subtype, cargo_subtype) {
    0: switch_xl25ml_graphics_subtype_0;
    1: spriteset_xl25ml_original;
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_graphics, position_in_articulated_veh % 3) {
    1: switch_xl25ml_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_xl25ml_capacity, position_in_articulated_veh % 3) {
    1: return 118;
    return 0;
}


item (FEAT_TRAINS, xl25ml) {
    property {
        // Menu
        name:                           string(STR_NAME_XL25ML);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					yz25ml;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1981,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [MAIL];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             MAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 71;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         42 ton;
        cost_factor:                    7;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            4;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_XL25ML), string(STR_25ML_LIVERY_AVAILABILITY), string(STR_DECAY_200));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_xl25ml_graphics;
        purchase:                       spriteset_xl25ml_purchase_original;
        cargo_subtype_text:             switch_xl25ml_cargo_subtype;
        articulated_part:               switch_xl25ml_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_xl25ml_capacity;
        cargo_age_period:               200 << param_cargo_decay;
        cost_factor:                    7;
        running_cost_factor:            5 * runningcostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        71/3;
        name:                            switch_xl25ml_name;

    }
}

// End XL25ML

