// Begin G50

// Graphics

// Purchase Menu
spriteset (spriteset_g50_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "g50")
}

// Vehicle
spriteset (spriteset_g50, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "g50")
}

switch (FEAT_TRAINS, SELF, switch_g50_articulated_part, extra_callback_info1) {
    1..2: return g50;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_g50_graphics, position_in_articulated_veh % 3) {
    1: spriteset_g50;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_g50_capacity, position_in_articulated_veh % 3) {
    1: return 50;
    return 0;
}


item (FEAT_TRAINS, g50) {
    property {
        // Menu
        name:                           string(STR_NAME_G50);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1958,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_LIQUID, CC_HAZARDOUS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 50;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          80 km/h;
        power:                          0 hp;
        weight:                         22.2 ton;
        cost_factor:                    5;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            4;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DECAY_185);
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_g50_graphics;
        purchase:                       spriteset_g50_purchase;
        articulated_part:               switch_g50_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_g50_capacity;
        cargo_age_period:               185 << param_cargo_decay;
        cost_factor:                    5;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        50/3;
    }
}

// End G50

