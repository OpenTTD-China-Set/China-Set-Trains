// Begin DF4D0000

// Graphics

// Purchase Menu
spriteset (spriteset_df4d0000_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df4d-0000")
}

// Vehicle
spriteset (spriteset_df4d0000_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4d-0000")
}
spriteset (spriteset_df4d0000_xihu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4d-xihu")
}
spriteset (spriteset_df4d0000_xihu_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4d-xihu-r")
}
spriteset (spriteset_df4d0000_qinhuangdao, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4d-qinhuangdao")
}

switch (FEAT_TRAINS, SELF, switch_df4d0000_articulated_part, extra_callback_info1) {
    1..2: return df4d0000;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df4d0000_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_XIHU);
    2: return string(STR_XIHU_REVERSE);
    3: return string(STR_QINHUANGDAO);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df4d0000_graphics_subtype, cargo_subtype) {
    0: spriteset_df4d0000_original;
    1: spriteset_df4d0000_xihu;
    2: spriteset_df4d0000_xihu_reversed;
    3: spriteset_df4d0000_qinhuangdao;
}

switch (FEAT_TRAINS, SELF, switch_df4d0000_graphics, position_in_articulated_veh % 3) {
    1: switch_df4d0000_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df4d0000) {
    property {
        // Menu
        name:                           string(STR_NAME_DF4D0000);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1997,01,01);
        model_life:                     50;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          145 km/h;
        power:                          2940 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.224;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              14;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_DF4D), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF4D0000_LIVERY_AVAILABILITY), string(STR_DF4D_NICKNAME), string(STR_RELIABILITY_14));
        cargo_subtype_text:             switch_df4d0000_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df4d0000_graphics;
        purchase:                       spriteset_df4d0000_purchase;
        articulated_part:               switch_df4d0000_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    79;
        running_cost_factor:            62 * runningcostfactor();
        purchase_running_cost_factor:   248;
        purchase_cargo_capacity:        0;
    }
}

// End DF4D0000

