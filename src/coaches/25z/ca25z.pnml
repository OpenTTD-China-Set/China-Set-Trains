// Begin CA25Z

// Graphics

// Purchase Menu
spriteset (spriteset_ca25z_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_ca25z_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "ca25z")
}

// Vehicle
spriteset (spriteset_ca25z_beijing) {
    template_empty ()
}
spriteset (spriteset_ca25z_guangzhou) {
    template_empty ()
}
spriteset (spriteset_ca25z_beijing_blue) {
    template_empty ()
}
alternative_sprites(spriteset_ca25z_beijing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25z")
}
alternative_sprites(spriteset_ca25z_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25z-guangzhou")
}
alternative_sprites (spriteset_ca25z_beijing_blue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25z-beijing-blue")
}

switch (FEAT_TRAINS, SELF, switch_ca25z_articulated_part, extra_callback_info1) {
    1..2: return ca25z;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ca25z_cargo_subtype, cargo_subtype) {
    0: return string(STR_BEIJING);
    1: return string(STR_GUANGSHEN);
    2: return string(STR_BLUE_RED_WHITE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca25z_graphics_subtype, cargo_subtype) {
    0: spriteset_ca25z_beijing;
    1: spriteset_ca25z_guangzhou;
    2: spriteset_ca25z_beijing_blue;
}

switch (FEAT_TRAINS, SELF, switch_ca25z_graphics, position_in_articulated_veh % 3) {
    1: switch_ca25z_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca25z_capacity, position_in_articulated_veh % 3) {
    1: return 36;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_ca25z_additional_text, param_cargo_decay) {
    return string(STR_DESC_4, string(STR_FULL_NAME_CA25Z), string(STR_CAFE_EFFECT), string(STR_25Z_LIVERY_AVAILABILITY), string(STR_COMFORT_192_CAFE));
}


item (FEAT_TRAINS, ca25z) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 36;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         46.3 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                switch_ca25z_additional_text;
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca25z_cargo_subtype;
        
        // Graphics
        default:                        switch_ca25z_graphics;
        purchase:                       spriteset_ca25z_purchase_original;
        articulated_part:               switch_ca25z_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25z_capacity;
        cargo_age_period:               (12 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    28;
        running_cost_factor:            6 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   74;
        purchase_cargo_capacity:        36/3;
    }
}

// End CA25Z

