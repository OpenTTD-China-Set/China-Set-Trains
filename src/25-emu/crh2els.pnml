// Begin CRH2ELS

// Graphics

// Purchase Menu
spriteset (spriteset_crh2els_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "crh2els-c")
}

// Vehicle
spriteset (spriteset_crh2els_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh2els-c")
}
spriteset (spriteset_crh2els_middle_m, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh2els-m")
}
spriteset (spriteset_crh2els_middle_ca, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh2els-ca")
}

spriteset (spriteset_crh2els_middle_p_a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh2els-tp-a")
}
spriteset (spriteset_crh2els_middle_p_b, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh2els-tp-b")
}
spriteset (spriteset_crh2els_cab, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh2els-c")
}



switch (FEAT_TRAINS, SELF, switch_crh2els_articulated_part, extra_callback_info1) {
    1..2: return crh2els;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_graphics, position_in_articulated_veh % 3) {
    1: spriteset_crh2els_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_head_capacity, position_in_articulated_veh % 3) {
    1: return 40;
    return 0;
}
//GFX
switch (FEAT_TRAINS, SELF, switch_crh2els_if_middle_ca, vehicle_type_id) {
    1035: spriteset_crh2els_middle_ca;
    spriteset_crh2els_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_middle_wagon_tp_long_back, (position_in_consist_from_end / 3) + 1) {
    4: spriteset_crh2els_middle_p_b;
    5: spriteset_crh2els_middle_p_a;
    switch_crh2els_if_middle_ca;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_middle_wagon_tp_long_front, (position_in_consist / 3) + 1) {
    4: spriteset_crh2els_middle_p_a;
    5: spriteset_crh2els_middle_p_b;
    switch_crh2els_if_middle_ca;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_if_middle_wagon_tp_front_or_back, (position_in_consist / 3) + 1 <= (num_vehs_in_consist / 6)) {
    1: switch_crh2els_middle_wagon_tp_long_front;
    switch_crh2els_middle_wagon_tp_long_back;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_if_middle_wagon_tp_short, (position_in_consist / 3) + 2 - (num_vehs_in_consist / 6)) {
    0: spriteset_crh2els_middle_p_a;
    1: spriteset_crh2els_middle_p_b;
    switch_crh2els_if_middle_ca;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_length_class, num_vehs_in_consist / 3) {
    2..12: switch_crh2els_if_middle_wagon_tp_short;
    switch_crh2els_if_middle_wagon_tp_front_or_back;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_if_wagon_end, position_in_consist_from_end == 1) {
    1: spriteset_crh2els_cab;
    switch_crh2els_length_class;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_graphics, (position_in_consist_from_end % 3) == 1) {
    1: switch_crh2els_if_wagon_end;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_length, position_in_consist_from_end) {
    switch_length_2_8_2;
}


switch (FEAT_TRAINS, SELF, switch_crh2els_middle_capacity_1, vehicle_type_id) {
    1035: return 20;
    1037: return 60;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_middle_capacity, position_in_articulated_veh % 3) {
    1: switch_crh2els_middle_capacity_1;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_cab_capacity_1, vehicle_type_id) {
    1035: return 8;
    1037: return 40;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_cab_capacity, position_in_articulated_veh % 3) {
    1: switch_crh2els_cab_capacity_1;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_capacity, position_in_consist_from_end) {
    0..2: switch_crh2els_cab_capacity;
    switch_crh2els_middle_capacity;
}


//cost
switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_running_power, vehicle_type_id) {
    1035: return 54;
    1037: return 54;
    return 0;
}


switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_running_unpower, vehicle_type_id) {
    1035: return 15;
    1037: return 15;
    return 0;
}


switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_running, vehicle_is_potentially_powered) {
    1: switch_crh2els_wagon_running_power;
    switch_crh2els_wagon_running_unpower;
}


switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_pw, position_in_consist % 12) {
    3..8: return 1529; // 1529 HP = 1140 KW
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_power, position_in_consist_from_end) {
    0..2: return 0;
    switch_crh2els_wagon_pw;
}

switch (FEAT_TRAINS, SELF, switch_crh2els_wagon_traction, vehicle_is_potentially_powered) {
    1: return int(0.108*255);
    return 0;
}


item (FEAT_TRAINS, crh2els) {
    property {
        // Menu
        name:                           string(STR_NAME_CRH2ELS);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					9220;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2017,03,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   25;
        track_type:                     SAAA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               640 << param_cargo_decay;

        // Stats
        speed:                          250 km/h;
        power:                          1200 kW;
        weight:                         51 ton;
        tractive_effort_coefficient:    0.075;
        cost_factor:                    45;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            50;
        reliability_decay:              12;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_CRH_LSLEEPER_CAN_ATTACH_WAGON), string(STR_CRH2G_NICKNAME), string(STR_RELIABILITY_12), string(STR_CRH2E_CONSIST), string(STR_CRH2ELS_HEAD_SEAT));
        can_attach_wagon:               crh_wagons_longitudinal_sleeper;
        start_stop:                     mu4car;
        // Graphics
        default:                        switch_crh2els_graphics;
        purchase:                       spriteset_crh2els_purchase_original;
        articulated_part:               switch_crh2els_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_crh2els_head_capacity;
        power:                          return 0;
        purchase_power:                 return 1609;
        tractive_effort_coefficient:    return 0;
        cargo_age_period:               12 * 5 * cafedecayfactor() << param_cargo_decay;
        cost_factor:                    50;
        running_cost_factor:            45 * runningcostfactor() * cafecostfactor() + 20;
        purchase_running_cost_factor:   200;
        purchase_cargo_capacity:        40/3;
    }

    // Multiple Unit Wagons
    livery_override(wymuw) {
        default:                        switch_crh2els_wagon_graphics;
        cargo_capacity:                 switch_crh2els_wagon_capacity;
        power:                          switch_crh2els_wagon_power;
        tractive_effort_coefficient:    switch_crh2els_wagon_traction;
        length:                         switch_crh2els_wagon_length;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        running_cost_factor:            switch_crh2els_wagon_running() * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(zecmuw) {
        default:                        switch_crh2els_wagon_graphics;
        cargo_capacity:                 switch_crh2els_wagon_capacity;
        power:                          switch_crh2els_wagon_power;
        tractive_effort_coefficient:    switch_crh2els_wagon_traction;
        length:                         switch_crh2els_wagon_length;
        weight:                         51;
        can_attach_wagon:               muhead;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cargo_age_period:               (800) << param_cargo_decay;//餐车效果
        running_cost_factor:            switch_crh2els_wagon_running() * runningcostfactor() + 50 + accostfactor(); // IT IS A RESTAURANT CAR!
    }
}

// End CRH2ELS
