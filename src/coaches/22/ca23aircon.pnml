//Begin CA23AIRCON
switch (FEAT_TRAINS, SELF, switch_ca23aircon_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_22_AIRCON_SERIES);
    return CB_RESULT_NO_TEXT;
}

// CA23 is a subtype of 22 type

// Graphics

// Purchase Menu
spriteset (spriteset_ca23aircon_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "ca23aircon")
}

// Vehicle
spriteset (spriteset_ca23aircon_beijing2shanghai, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca23aircon")
}
spriteset (spriteset_ca23aircon_alternative, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca23aircon-alternative")
}
spriteset (spriteset_ca23aircon_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca23aircon-guangzhou")
}

switch (FEAT_TRAINS, SELF, switch_ca23aircon_articulated_part, extra_callback_info1) {
    1..2: return ca23aircon;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ca23aircon_cargo_subtype, cargo_subtype) {
    0: return string(STR_BEIJING2SHANGHAI);
    1: return string(STR_BEIJING2SHANGHAIALT);
    2: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca23aircon_graphics_subtype, cargo_subtype) {
    0: spriteset_ca23aircon_beijing2shanghai;
    1: spriteset_ca23aircon_alternative;
    2: spriteset_ca23aircon_guangzhou;
}

switch (FEAT_TRAINS, SELF, switch_ca23aircon_graphics, position_in_articulated_veh % 3) {
    1: switch_ca23aircon_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca23aircon_capacity, position_in_articulated_veh % 3) {
    1: return 48;
    return 0;
}


item (FEAT_TRAINS, ca23aircon) {
    property {
        // Menu
        name:                           string(STR_NAME_CA23AIRCON);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					yz22aircon;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1985,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 48;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         44.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA23AIRCON), string(STR_CAFE_EFFECT), string(STR_22AIRCON_LIVERY_AVAILABILITY), string(STR_COMFORT_160_CAFE));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca23aircon_cargo_subtype;

        // Graphics
        default:                        switch_ca23aircon_graphics;
        purchase:                       spriteset_ca23aircon_purchase_original;
        articulated_part:               switch_ca23aircon_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 switch_ca23aircon_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    25;
        running_cost_factor:            4 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   66;
        purchase_cargo_capacity:        48/3;
        name:                            switch_ca23aircon_name;

    }
}

// End CA23AIRCON

