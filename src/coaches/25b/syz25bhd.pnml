// Begin SYZ25BHD

// Graphics

// Purchase Menu
spriteset (spriteset_s25bhd_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "s25bhd-deteriorate")
}

// Vehicle
spriteset (spriteset_s25bhd_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "s25bhd-deteriorate") // Unfinished
}
spriteset (spriteset_s25bhd_deteriorate, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "s25bhd-deteriorate")
}
spriteset (spriteset_s25bhd_shenyang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "s25bhd-shenyang")
}

switch (FEAT_TRAINS, SELF, switch_syz25bhd_articulated_part, extra_callback_info1) {
    1..2: return syz25bhd;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_s25bhd_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL); // Unfinished
    1: return string(STR_DETERIORATE);
    2: return string(STR_SHENYANG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_s25bhd_graphics_subtype, cargo_subtype) {
    0: spriteset_s25bhd_original; // Unfinished
    1: spriteset_s25bhd_deteriorate;
    2: spriteset_s25bhd_shenyang;
}

switch (FEAT_TRAINS, SELF, switch_s25bhd_graphics, position_in_articulated_veh % 3) {
    1: switch_s25bhd_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_syz25bhd_capacity, position_in_articulated_veh % 3) {
    1: return 176;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_syz25bhd_additional_text, param_cargo_decay) {
    return string(STR_DESC_3, string(STR_FULL_NAME_SYZ25B), string(STR_S25BHD_LIVERY_AVAILABILITY), string(STR_COMFORT_160));
}


item (FEAT_TRAINS, syz25bhd) {
    property {
        // Menu
        name:                           string(STR_NAME_SYZ25BHD);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1994,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         52.2 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                switch_syz25bhd_additional_text;
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_s25bhd_cargo_subtype;

        // Graphics
        default:                        switch_s25bhd_graphics;
        purchase:                       spriteset_s25bhd_purchase_original;
        articulated_part:               switch_syz25bhd_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_syz25bhd_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    10;
        running_cost_factor:            8 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   32;
        purchase_cargo_capacity:        176/3;
    }
}

// End SYZ25BHD

