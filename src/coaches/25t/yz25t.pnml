// Begin YZ25T
switch (FEAT_TRAINS, SELF, switch_yz25t_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25T_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_yz25t_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25t/yz25t")
}

// Vehicle
spriteset (spriteset_yz25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yz25t")
}
spriteset (spriteset_yz25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yz25t-green")
}
spriteset (spriteset_yz25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yz25t-xz")
}
spriteset (spriteset_bspyz25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bspyz25t-xz")
}

switch (FEAT_TRAINS, SELF, switch_yz25t_articulated_part, extra_callback_info1) {
    1..2: return yz25t;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_yz25t_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    2: string(STR_GAOZU_GREEN);
    3: string(STR_QINGZANG);
    4: string(STR_QINGZANG_BSP);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_yz25t_graphics_subtype_year_depend, build_year) {
     0..2014: spriteset_yz25t_original;
    spriteset_yz25t_green;
}
switch (FEAT_TRAINS, SELF, switch_yz25t_graphics_subtype_0, param_gaozulv) {
    1: switch_yz25t_graphics_subtype_year_depend;
    spriteset_yz25t_original;
}
switch (FEAT_TRAINS, SELF, switch_yz25t_graphics_subtype, cargo_subtype) {
    0: switch_yz25t_graphics_subtype_0;
    1: spriteset_yz25t_original;
    2: spriteset_yz25t_green;
    3: spriteset_yz25t_qingzang;
    4: spriteset_bspyz25t_qingzang;
}

switch (FEAT_TRAINS, SELF, switch_yz25t_graphics, position_in_articulated_veh % 3) {
    1: switch_yz25t_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_yz25t_capacity, position_in_articulated_veh % 3) {
    1: return 118;
    return 0;
}


item (FEAT_TRAINS, yz25t) {
    property {
        // Menu
        name:                           string(STR_NAME_YZ25T);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2004,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          0 hp;
        weight:                         50 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_YZ25T), string(STR_YZ_YW25T_LIVERY_AVAILABILITY), string(STR_COMFORT_LOW, 160));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_yz25t_cargo_subtype;

        // Graphics
        default:                        switch_yz25t_graphics;
        purchase:                       spriteset_yz25t_purchase_original;
        articulated_part:               switch_yz25t_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_yz25t_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    9;
        running_cost_factor:            8 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   32;
        purchase_cargo_capacity:        118/3;
        name:                           switch_yz25t_name;
    }
}

// End YZ25T

