//Begin DF9

// Graphics

// Purchase Menu
spriteset (spriteset_df9_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df9")
}

// Vehicle
spriteset (spriteset_df9_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df9")
}

spriteset (spriteset_df9_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df9-red")
}

spriteset (spriteset_df9_lightblue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df9-lightblue")
}

switch (FEAT_TRAINS, SELF, switch_df9_articulated_part, extra_callback_info1) {
    1..2: return df9;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df9_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_RED);
    2: return string(STR_LAKEBLUE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df9_graphics_subtype, cargo_subtype) {
    0: spriteset_df9_original;
    1: spriteset_df9_red;
    2: spriteset_df9_lightblue;
}

switch (FEAT_TRAINS, SELF, switch_df9_graphics, position_in_articulated_veh % 3) {
    1: switch_df9_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df9) {
    property {
        // Menu
        name:                           string(STR_NAME_DF9);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1990,01,01);
        model_life:                     50;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          3610 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.181;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_DF9), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_MEDIUM, 16));
        cargo_subtype_text:             switch_df9_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df9_graphics;
        purchase:                       spriteset_df9_purchase;
        articulated_part:               switch_df9_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    91;
        running_cost_factor:            71 * runningcostfactor();
        purchase_running_cost_factor:   284;
        purchase_cargo_capacity:        0;

    }
}

// End DF9

