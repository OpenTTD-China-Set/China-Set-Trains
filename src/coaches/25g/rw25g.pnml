//Begin RW25G
switch (FEAT_TRAINS, SELF, switch_rw25g_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25G_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_rw25g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25g", "rw25g")
}

// Vehicle
spriteset (spriteset_rw25g_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g")
}
spriteset (spriteset_rw25g_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-green")
}
spriteset (spriteset_rw25g_jinwen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-jinwen-1")
}
spriteset (spriteset_rw25g_jinwen_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-jinwen-green-1")
}
spriteset (spriteset_rw25g_gmsr, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-gmsr")
}
spriteset (spriteset_rw25g_gmsr_early, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-gmsr-early")
}
spriteset (spriteset_rw25g_hejiu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-hejiu")
}
spriteset (spriteset_rw25g_fulong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25g", "rw25g-fulong")
}

switch (FEAT_TRAINS, SELF, switch_rw25g_articulated_part, extra_callback_info1) {
    1..2: return rw25g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_rw25g_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    2: return string(STR_GAOZU_GREEN);
    3: return string(STR_JINWEN);
    4: return string(STR_JINWEN_GREEN);
    5: return string(STR_GMSR);
    6: return string(STR_GMSR_EARLY);
    7: return string(STR_HEJIU);
    8: return string(STR_FULONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_rw25g_graphics_subtype_year_depend, build_year) {
    0..2014: spriteset_rw25g_original; // Unfinished
    spriteset_rw25g_green;
}
switch (FEAT_TRAINS, SELF, switch_rw25g_graphics_subtype_0, param_gaozulv) {
    1: switch_rw25g_graphics_subtype_year_depend;
    spriteset_rw25g_original;
}
switch (FEAT_TRAINS, SELF, switch_rw25g_graphics_subtype, cargo_subtype) {
    0: switch_rw25g_graphics_subtype_0;
    1: spriteset_rw25g_original;
    2: spriteset_rw25g_green;
    3: spriteset_rw25g_jinwen;
    4: spriteset_rw25g_jinwen_green;
    5: spriteset_rw25g_gmsr;
    6: spriteset_rw25g_gmsr_early;
    7: spriteset_rw25g_hejiu;
    8: spriteset_rw25g_fulong;
}

switch (FEAT_TRAINS, SELF, switch_rw25g_graphics, position_in_articulated_veh % 3) {
    1: switch_rw25g_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_rw25g_capacity, position_in_articulated_veh % 3) {
    1: return 36;
    return 0;
}


item (FEAT_TRAINS, rw25g) {
    property {
        // Menu
        name:                           string(STR_NAME_RW25G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2048;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 36;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               444 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         47.5 ton;
        cost_factor:                    13;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            11;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_RW25G), string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVALIABLE), string(STR_ORIGINAL), string(STR_GAOZU_GREEN), string(STR_JINWEN), string(STR_JINWEN_GREEN), string(STR_GMSR), string(STR_GMSR_EARLY), string(STR_HEJIU),  string(STR_FULONG)), string(STR_COMFORT_HIGH, 384));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_rw25g_graphics;
        purchase:                       spriteset_rw25g_purchase_original;
        articulated_part:               switch_rw25g_articulated_part;
        cargo_subtype_text:             switch_rw25g_cargo_subtype;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_rw25g_capacity;
        cargo_age_period:               (24 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    14;
        running_cost_factor:            11 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        36/3;
        name:                            switch_rw25g_name;

    }
}

// End RW25G

