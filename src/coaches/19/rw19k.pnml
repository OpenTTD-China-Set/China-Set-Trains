//Begin RW19K
switch (FEAT_TRAINS, SELF, switch_rw19k_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25K_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_rw19k_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches","rw19k-blue-folddoor")
}

// Vehicle
spriteset (spriteset_rw19k_blue_folddoor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "rw19k-blue-folddoor")
}
spriteset (spriteset_rw19k_red_folddoor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "rw19k-red-folddoor")
}
spriteset (spriteset_rw19k_blue_plugdoor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "rw19k-blue-plugdoor")
}
spriteset (spriteset_rw19k_red_plugdoor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "rw19k-red-plugdoor")
}

switch (FEAT_TRAINS, SELF, switch_rw19k_articulated_part, extra_callback_info1) {
    1..2: return rw19k;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

random_switch (FEAT_TRAINS, SELF, switch_rw19k_blue) {
    1: spriteset_rw19k_blue_folddoor;
    1: spriteset_rw19k_blue_plugdoor;
}

random_switch (FEAT_TRAINS, SELF, switch_rw19k_red) {
    1: spriteset_rw19k_red_folddoor;
    1: spriteset_rw19k_red_plugdoor;
}

switch (FEAT_TRAINS, SELF, switch_rw19k_graphics_subtype, cargo_subtype) {
    0: switch_rw19k_blue;
    1: switch_rw19k_red;
}

switch (FEAT_TRAINS, SELF, switch_rw19k_graphics, position_in_articulated_veh % 3) {
    1: switch_rw19k_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_rw19k_cargo_subtype, cargo_subtype) {
    0: return string(STR_BLUE_RED_WHITE);
    1: return string(STR_RED_WHITE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_rw19k_capacity, position_in_articulated_veh % 3) {
    1: return 16;
    return 0;
}


item (FEAT_TRAINS, rw19k) {
    property {
        // Menu
        name:                           string(STR_NAME_RW19K);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2058;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1997,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 16;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          0 hp;
        weight:                         50 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_RW19K), string(STR_19K_LIVERY_AVAILABILITY), string(STR_COMFORT_640));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_rw19k_cargo_subtype;

        // Graphics
        default:                        switch_rw19k_graphics;
        purchase:                       spriteset_rw19k_purchase;
        articulated_part:               switch_rw19k_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_rw19k_capacity;
        cargo_age_period:               (40 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    17;
        running_cost_factor:            14 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   56;
        purchase_cargo_capacity:        16/3;
        name:                            switch_rw19k_name;

    }
}

// End RW19K

