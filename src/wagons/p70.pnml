//Begin P70
switch (FEAT_TRAINS, SELF, switch_p70_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BOXCAR);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_p70_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p70-black")
}

// Vehicle
spriteset (spriteset_p70_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p70-black")
}
spriteset (spriteset_p70_rustyred, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p70-rustyred")
}
spriteset (spriteset_p70_blue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p70-blue")
}
spriteset (spriteset_p70_brown, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p70-brown")
}


/* random_switch (FEAT_TRAINS, SELF, random_p70_colour) {
    1: spriteset_p70_black;
    1: spriteset_p70_rustyred;
    1: spriteset_p70_blue;
    1: spriteset_p70_brown;
}
 */
random_switch (FEAT_TRAINS, SELF, random_p70_colour_early) {
    15: spriteset_p70_black;
    10: spriteset_p70_brown;
    1: spriteset_p70_rustyred;
    5: spriteset_p70_blue;
}
random_switch (FEAT_TRAINS, SELF, random_p70_colour_middium) {
    10: spriteset_p70_black;
    3: spriteset_p70_blue;
    10: spriteset_p70_brown;
    5: spriteset_p70_rustyred;
}
random_switch (FEAT_TRAINS, SELF, random_p70_colour_late) {
    10: spriteset_p70_black;
    10: spriteset_p70_brown;
    10: spriteset_p70_rustyred;
    1: spriteset_p70_blue;
}
switch (FEAT_TRAINS, SELF, random_p70_colour, current_year - 2002) {
    0..15: random_p70_colour_early;
    16..30: random_p70_colour_middium;
    random_p70_colour_late;
}

switch (FEAT_TRAINS, SELF, switch_p70_articulated_part, extra_callback_info1) {
    1..2: return p70;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p70_graphics, position_in_articulated_veh % 3) {
    1: random_p70_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_p70_capacity_cargo, cargo_type_in_veh) {
    MAIL: return 140;
    GOOD: return 105;
    return 70;
}

switch (FEAT_TRAINS, SELF, switch_p70_capacity, position_in_articulated_veh % 3) {
    1: switch_p70_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p70_loading_speed, cargo_type_in_veh) {
    MAIL: return 10;
    GOOD: return 8;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p70_decay, cargo_type_in_veh) {
    LVST: return 560;
    return 185;
}


item (FEAT_TRAINS, p70) {
    property {
        // Menu
        name:                           string(STR_NAME_P70);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					p1;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2003,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SADN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED, CC_EXPRESS, CC_PIECE_GOODS, CC_COVERED);
        non_refittable_cargo_classes:   bitmask(CC_LIQUID, CC_HAZARDOUS, CC_OVERSIZED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [YETI, YETY];
        refit_cost:                     0;
        default_cargo_type:             GOOD;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 70;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         24.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_P70_LIVERY_AVAILABILITY), string(STR_DECAY_BOXCAR_NOPASS));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_p70_graphics;
        purchase:                       spriteset_p70_purchase_original;
        articulated_part:               switch_p70_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 switch_p70_capacity;
        loading_speed:                  switch_p70_loading_speed() << param_loading_speed;
        cargo_age_period:               switch_p70_decay() << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    6;
        running_cost_factor:            5 * runningcostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        70/3;
        name:                            switch_p70_name;

    }
}

// End P70

