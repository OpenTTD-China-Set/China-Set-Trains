//Begin SRZ25BHD
spriteset (spriteset_srz25bhd_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25b/srz25bhd-dacheng")
}

//Graphics
spriteset (spriteset_srz25bhd_dacheng, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/srz25bhd-dacheng")
}
spriteset (spriteset_srz25bhd_urumqi, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/srz25bhd-urumqi")
}
spriteset (spriteset_srz25bhd_orange, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/srz25bhd-orange")
}

switch (FEAT_TRAINS, SELF, switch_srz25bhd_articulated_part, extra_callback_info1) {
    1..2: return srz25bhd;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_TRAINS, SELF, switch_srz25bhd_cargo_subtype, cargo_subtype) {
    0: return string(STR_DACHENG);
    1: return string(STR_URUMQI);
    2: return string(STR_ORANGE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_srz25bhd_graphics_subtype, cargo_subtype) {
    0: spriteset_srz25bhd_dacheng;
    1: spriteset_srz25bhd_urumqi;
    2: spriteset_srz25bhd_orange;
}

switch (FEAT_TRAINS, SELF, switch_srz25bhd_graphics, position_in_articulated_veh % 3) {
    1: switch_srz25bhd_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_srz25bhd_capacity, position_in_articulated_veh % 3) {
    1: return 108;
    return 0;
}


item (FEAT_TRAINS, srz25bhd) {
    property {
        // Menu
        name:                           string(STR_NAME_SRZ25BHD);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					syz25bld;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1994,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         51.9 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SRZ25B), string(STR_AIR_CONDITIONER_NEED_POWER), string(STR_LV_AVAILABLE_S , string(STR_LIVERY_AVALIABLE), string(STR_DACHENG), string(STR_URUMQI), string(STR_ORANGE)), string(STR_COMFORT_MEDIUM, 192));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_srz25bhd_cargo_subtype;

        // Graphics
        default:                        switch_srz25bhd_graphics;
        purchase:                       spriteset_srz25bhd_purchase;
        articulated_part:               switch_srz25bhd_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_srz25bhd_capacity;
        cargo_age_period:               (12 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    10;
        running_cost_factor:            8 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   32;
        purchase_cargo_capacity:        108/3;
        name:                           switch_s25b_name;

    }
}

// End SRZ25BHD

