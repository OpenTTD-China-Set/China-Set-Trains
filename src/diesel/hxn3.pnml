// Begin HXN3

// Graphics

// Purchase Menu
spriteset (spriteset_hxn3_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "hxn3")
}

// Vehicle
spriteset (spriteset_hxn3_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn3")
}

spriteset (spriteset_hxn3_guoneng, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn3-guoneng")
}

spriteset (spriteset_hxn3_laos, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn3-laos")
}

switch (FEAT_TRAINS, SELF, switch_hxn3_articulated_part, extra_callback_info1) {
    1..2: return hxn3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxn3_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    3: return string(STR_GUONENG);
    6: return string(STR_CHN2LAOS);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_hxn3_graphics_subtype, cargo_subtype) {
    0: spriteset_hxn3_original;
    3: spriteset_hxn3_guoneng;
    6: spriteset_hxn3_laos;
}

switch (FEAT_TRAINS, SELF, switch_hxn3_graphics, position_in_articulated_veh % 3) {
    1: switch_hxn3_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, hxn3) {
    property {
        // Menu
        name:                           string(STR_NAME_HXN3);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2008,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          4660 kW;
        weight:                         150 ton;
        tractive_effort_coefficient:    0.422;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              8;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_HXN3), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_8));
        cargo_subtype_text:             switch_hxn3_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_hxn3_graphics;
        purchase:                       spriteset_hxn3_purchase;
        articulated_part:               switch_hxn3_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cost_factor:                    98;
        running_cost_factor:            83 * runningcostfactor();
        purchase_running_cost_factor:   332;
        purchase_cargo_capacity:        0;
    }
}

// End HXN3

