// Begin DFH3

// Graphics

spriteset (spriteset_dfh3_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "dfh3")
}

spriteset (spriteset_dfh3_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "dfh3")
}

spriteset (spriteset_dfh3_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "dfh3-red")
}

spriteset (spriteset_dfh3_gongqingtuan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "dfh3-gongqingtuan")
}

spriteset (spriteset_dfh3_liaodong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "dfh3-liaodong")
}

switch (FEAT_TRAINS, SELF, switch_dfh3_articulated_part, extra_callback_info1) {
    1..2: return dfh3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_dfh3_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_RED);
    2: return string(STR_GONGQINGTUAN);
    3: return string(STR_LIAODONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_dfh3_graphics_subtype, cargo_subtype) {
    0: spriteset_dfh3_original;
    1: spriteset_dfh3_red;
    2: spriteset_dfh3_gongqingtuan;
    3: spriteset_dfh3_liaodong;
}

switch (FEAT_TRAINS, SELF, switch_dfh3_graphics, position_in_articulated_veh % 3) {
    1: switch_dfh3_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, dfh3) {
    property {
        // Menu
        name:                           string(STR_NAME_DFH3);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1971,01,01);
        model_life:                     60;
        retire_early:                   30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          1840 kW;
        weight:                         88 ton;
        tractive_effort_coefficient:    0.273;
        cost_factor:                    72;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            53;
        reliability_decay:              8;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_DFH3), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_LOW, 8));
        cargo_subtype_text:             switch_dfh3_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_dfh3_graphics;
        purchase:                       spriteset_dfh3_purchase;
        articulated_part:               switch_dfh3_articulated_part;

        // Stats
        length:                         switch_length_2_5_2;
        cargo_capacity:                 0;
        cost_factor:                    72;
        running_cost_factor:            53 * runningcostfactor();
        purchase_running_cost_factor:   212;
        purchase_cargo_capacity:        0;
    }
}

// End DFH3

