// Begin SL6

// Graphics

spriteset (spriteset_sl6_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase_xl ("steam/extralength", "sl6-l")
}

spriteset (spriteset_sl6_l_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("steam", "sl6-l-front")
}
spriteset (spriteset_sl6_s_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("steam", "sl6-s-front")
}
spriteset (spriteset_sl6_n_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("steam", "sl6-n-front")
}


spriteset (spriteset_sl6_tender, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("steam", "sl6-tender")
}


switch (FEAT_TRAINS, SELF, switch_sl6_cargo_subtype, cargo_subtype) {
    0: return string(STR_LARGE_WINDSHIELD);
    1: return string(STR_SMALL_WINDSHIELD);
    2: return string(STR_WITHOUT_WINDSHIELD);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_sl6_articulated_part, extra_callback_info1) {
    1..2: return sl6;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_gfx_sl6_front, cargo_subtype) {
    0: spriteset_sl6_l_front;
    1: spriteset_sl6_s_front;
    2: spriteset_sl6_n_front;
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_sl6_graphics, position_in_articulated_veh % 3) {
    1: switch_gfx_sl6_front;
    2: spriteset_sl6_tender;
    empty_sprites;
}


item (FEAT_TRAINS, sl6) {
    property {
        // Don't Change - It is crucial
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        climates_available:             ALL_CLIMATES;

        // Don't Change - Unless it is not a steam locomotive
        engine_class:                   ENGINE_CLASS_STEAM;
        running_cost_base:              RUNNING_COST_STEAM;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        // This is for freight locos
        ai_special_flag:                AI_FLAG_CARGO;

        // Don't Change - It is useless
        cargo_capacity:                 1;
        cost_factor:                    1;
        running_cost_factor:            1;

        // Change them accordingly
        name:                           string(STR_NAME_SL6);
        introduction_date:              date(1956,01,01);
        model_life:                     60;
        retire_early:                   40;
        vehicle_life:                   35;
        track_type:                     SABN;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;
        speed:                          110 km/h;
        power:                          1475 hpM;
        weight:                         172.57 ton;
        tractive_effort_coefficient:    0.0979;
        reliability_decay:              20;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SL6), string(STR_ALIASNAME_SL6), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_HIGH, 20));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_sl6_cargo_subtype;

        // Graphics
        default:                        switch_sl6_graphics;
        purchase:                       spriteset_sl6_purchase;
        articulated_part:               switch_sl6_articulated_part;

        // Stats
        length:                         switch_length_2_6_6;
        cargo_capacity:                 0;
        cost_factor:                    56;
        running_cost_factor:            39 * runningcostfactor();
        purchase_running_cost_factor:   156;
        purchase_cargo_capacity:        0;
    }
}

// End sl6
