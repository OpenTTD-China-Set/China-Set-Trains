// Begin SRZ225Z
switch (FEAT_TRAINS, SELF, switch_srz225z_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_S25Z_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_srz225z_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "srz225z")
}

// Vehicle
spriteset (spriteset_srz225z_beijing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches","srz225z")
}
spriteset (spriteset_srz225z_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "srz225z-guangzhou")
}

switch (FEAT_TRAINS, SELF, switch_srz225z_articulated_part, extra_callback_info1) {
    1..2: return srz225z;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_srz225z_graphics_subtype, cargo_subtype) {
    0: spriteset_srz225z_beijing;
    1: spriteset_srz225z_guangzhou;
}

switch (FEAT_TRAINS, SELF, switch_srz225z_graphics, position_in_articulated_veh % 3) {
    1: switch_srz225z_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_srz225z_cargo_subtype, cargo_subtype) {
    0: return string(STR_BEIJING);
    1: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_srz225z_capacity, position_in_articulated_veh % 3) {
    1: return 124;
    return 0;
}


item (FEAT_TRAINS, srz225z) {
    property {
        // Menu
        name:                           string(STR_NAME_SRZ225Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 124;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         52.7 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_SRZ225Z), string(STR_S25Z_LIVERY_AVAILABILITY), string(STR_COMFORT, 192));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_srz225z_cargo_subtype;

        // Graphics
        default:                        switch_srz225z_graphics;
        purchase:                       spriteset_srz225z_purchase;
        articulated_part:               switch_srz225z_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_srz225z_capacity;
        cargo_age_period:               (12 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    10;
        running_cost_factor:            9 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   36;
        purchase_cargo_capacity:        124/3;
        name:                            switch_srz225z_name;
    }
}

// End SRZ225Z

