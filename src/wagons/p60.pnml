//Begin P60
switch (FEAT_TRAINS, SELF, switch_p60_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BOXCAR);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_p60_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p60")
}

// Vehicle
spriteset (spriteset_p60_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p60")
}

random_switch (FEAT_TRAINS, SELF, random_p60_colour) {
    1: spriteset_p60_black; // More colours waiting to be added
}

switch (FEAT_TRAINS, SELF, switch_p60_articulated_part, extra_callback_info1) {
    1..2: return p60;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p60_graphics, position_in_articulated_veh % 3) {
    1: random_p60_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_p60_capacity_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 192;
    return 60;
}

switch (FEAT_TRAINS, SELF, switch_p60_capacity_cargo, cargo_classes == bitmask(CC_MAIL)) {
    1: return 120;
    switch_p60_capacity_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p60_capacity, position_in_articulated_veh % 3) {
    1: switch_p60_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p60_loading_speed_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 12;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p60_loading_speed, cargo_classes == bitmask(CC_MAIL)) {
    1: return 8;
    switch_p60_loading_speed_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p60_decay_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 80;
    return 185;
}

switch (FEAT_TRAINS, SELF, switch_p60_decay, cargo_type_in_veh == LVST) {
    1: return 560;
    switch_p60_decay_pengdaike;
}


item (FEAT_TRAINS, p60) {
    property {
        // Menu
        name:                           string(STR_NAME_P60);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3098;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1965,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_EXPRESS, CC_PIECE_GOODS, CC_COVERED);
        non_refittable_cargo_classes:   bitmask(CC_LIQUID, CC_HAZARDOUS, CC_OVERSIZED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [TOUR];
        refit_cost:                     0;
        default_cargo_type:             GOOD;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         22.2 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_P60_LIVERY_AVAILABILITY), string(STR_DECAY_BOXCAR, 185, 80, 560), string(STR_DESC_PENGDAIKE));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_p60_graphics;
        purchase:                       spriteset_p60_purchase_original;
        articulated_part:               switch_p60_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 switch_p60_capacity;
        loading_speed:                  switch_p60_loading_speed() << param_loading_speed;
        cargo_age_period:               switch_p60_decay() << param_cargo_decay;
        cost_factor:                    5;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        60/3;
        name:                           switch_p60_name;

    }
}

// End P60

