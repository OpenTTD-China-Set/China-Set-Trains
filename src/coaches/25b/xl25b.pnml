//Begin XL25B
switch (FEAT_TRAINS, SELF, switch_xl25b_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25B_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_xl25b_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25b/xl25b")
}

// Vehicle
spriteset (spriteset_xl25b_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/xl25b")
}
spriteset (spriteset_xl25b_xiyan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/xl25b-xiyan")
}
spriteset (spriteset_xl25b_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/xl25b-red")
}
spriteset (spriteset_xl25b_jy, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/xl25b-jy")
}

switch (FEAT_TRAINS, SELF, switch_xl25b_articulated_part, extra_callback_info1) {
    1..2: return xl25b;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORDINARY_GREEN);
/*     2: return string(STR_GAOZU_GREEN); */
    2: return string(STR_XIYAN);
    3: return string(STR_RED_WHITE);
    4: return string(STR_RESCUE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_graphics_build_year_depend, build_year) {
    0..2014: spriteset_uz25b_original; // Unfinished
    spriteset_uz25b_original;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_cargo_subtype_0, param_gaozulv) {
    1: switch_xl25b_graphics_build_year_depend;
    spriteset_xl25b_original;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_graphics_subtype, cargo_subtype) {
    0: switch_xl25b_cargo_subtype_0;
    1: spriteset_xl25b_original;
/*     2: ; */
    2: spriteset_xl25b_xiyan;
    3: spriteset_xl25b_red;
    4: spriteset_xl25b_jy;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_graphics, position_in_articulated_veh % 3) {
    1: switch_xl25b_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_xl25b_capacity, position_in_articulated_veh % 3) {
    1: return 68;
    return 0;
}


item (FEAT_TRAINS, xl25b) {
    property {
        // Menu
        name:                           string(STR_NAME_XL25B);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2077;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [MAIL, VALU, GOLD, DIAM];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             MAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 68;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         44.2 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_XL25B), string(STR_LV_AVAILABLE_S ,string(STR_LIVERY_AVAILABLE), string(STR_ORIGINAL)/* , string(STR_GAOZU_GREEN) */, string(STR_XIYAN), string(STR_RED_WHITE), string(STR_RESCUE)), string(STR_FRESHNESS_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_xl25b_cargo_subtype;

        // Graphics
        default:                        switch_xl25b_graphics;
        purchase:                       spriteset_xl25b_purchase_original;
        articulated_part:               switch_xl25b_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_xl25b_capacity;
        cargo_age_period:               200 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    7;
        running_cost_factor:            5 * runningcostfactor() * cafecostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        68/3;
        name:                            switch_xl25b_name;

    }
}

// End XL25B

