// Begin CRH6F

// Graphics

// Purchase Menu
spriteset (spriteset_crh6f_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "crh6f-c-foshan")
}

// Vehicle
spriteset (spriteset_crh6f_front_foshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-c-foshan")
}
spriteset (spriteset_crh6f_middle_m_foshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-m-foshan")
}
spriteset (spriteset_crh6f_middle_p_foshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-p-foshan")
}
spriteset (spriteset_crh6f_cab_foshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh6f-c-foshan")
}


spriteset (spriteset_crh6f_front_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-c-jinshan")
}
spriteset (spriteset_crh6f_middle_m_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-m-jinshan")
}
spriteset (spriteset_crh6f_middle_p_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-p-jinshan")
}
spriteset (spriteset_crh6f_cab_jinshan, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh6f-c-jinshan")
}


spriteset (spriteset_crh6f_front_xiaoyong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-c-xiaoyong")
}
spriteset (spriteset_crh6f_middle_m_xiaoyong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-m-xiaoyong")
}
spriteset (spriteset_crh6f_middle_p_xiaoyong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh6f-p-xiaoyong")
}
spriteset (spriteset_crh6f_cab_xiaoyong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh6f-c-xiaoyong")
}


switch (FEAT_TRAINS, SELF, switch_crh6f_articulated_part, extra_callback_info1) {
    1..2: return crh6f;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_cargo_subtype, cargo_subtype) {
    0: return string(STR_ZHUSANJIAO_ORANGE);
    1: return string(STR_JINSHAN);
    2: return string(STR_XIAOYONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_front, cargo_subtype) {
    0: spriteset_crh6f_front_foshan;
    1: spriteset_crh6f_front_jinshan;
    2: spriteset_crh6f_front_xiaoyong;
}

switch (FEAT_TRAINS, PARENT, switch_crh6f_middle_p, cargo_subtype) {
    0: spriteset_crh6f_middle_p_foshan;
    1: spriteset_crh6f_middle_p_jinshan;
    2: spriteset_crh6f_middle_p_xiaoyong;
}

switch (FEAT_TRAINS, PARENT, switch_crh6f_middle_m, cargo_subtype) {
    0: spriteset_crh6f_middle_m_foshan;
    1: spriteset_crh6f_middle_m_jinshan;
    2: spriteset_crh6f_middle_m_xiaoyong;
}

switch (FEAT_TRAINS, PARENT, switch_crh6f_cab, cargo_subtype) {
    0: spriteset_crh6f_cab_foshan;
    1: spriteset_crh6f_cab_jinshan;
    2: spriteset_crh6f_cab_xiaoyong;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6f_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_head_capacity, position_in_articulated_veh % 3) {
    1: return 216;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_short_graphics, (position_in_consist_from_end == position_in_consist) || (position_in_consist_from_end == position_in_consist + 3)) {
    1: switch_crh6f_middle_p;
    switch_crh6f_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_long_graphics, (position_in_consist_from_end == position_in_consist + 12) || (position_in_consist_from_end == position_in_consist + 15) || (position_in_consist_from_end + 12 == position_in_consist) || (position_in_consist_from_end + 15 == position_in_consist)) {
    1: switch_crh6f_middle_p;
    switch_crh6f_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_middle_graphics_1, num_vehs_in_consist) {
    0..24: switch_crh6f_short_graphics;
    switch_crh6f_long_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_middle_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6f_middle_graphics_1;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_cab_graphics, position_in_articulated_veh % 3) {
    1: switch_crh6f_cab;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_graphics, position_in_consist_from_end) {
    0..2: switch_crh6f_cab_graphics;
    switch_crh6f_middle_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_middle_capacity, position_in_articulated_veh % 3) {
    1: return 281;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_capacity, position_in_consist_from_end) {
    1: return 216;
    switch_crh6f_middle_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_cost, vehicle_is_potentially_powered) {
    1: return 66;
    return 25;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_running, vehicle_is_potentially_powered) {
    1: return 52;
    return 11;
}

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_power, position_in_consist % 24) {
    0..2: return 0;
    3..5: return 1462;
    6..8: return 0;
    9..14: return 1462;
    15..17: return 0;
    18..20: return 1462;
    21..23: return 0;
    return 0;
} // T-M-T-M-M-T-M-T

switch (FEAT_TRAINS, SELF, switch_crh6f_wagon_traction, vehicle_is_potentially_powered) {
    1: return int(0.2*255);
    return 0;
}


item (FEAT_TRAINS, crh6f) {
    property {
        // Menu
        name:                           string(STR_NAME_CRH6F);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					9228;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2013,07,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SABA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  18 << param_loading_speed;
        cargo_age_period:               120 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          1224.5 kW;
        weight:                         48 ton;
        tractive_effort_coefficient:    0.2;
        cost_factor:                    61;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            26;
        reliability_decay:              6;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_CRH6F_CAN_ATTACH_WAGON), string(STR_CRH6F_NICKNAME), string(STR_CRH6F_LIVERY_AVAILABILITY), string(STR_RELIABILITY_6), string(STR_CRH6F_CONSIST), string(STR_CRH6F_HEAD_SEAT));
        can_attach_wagon:               onlyze;
        start_stop:                     mu4car;
        cargo_subtype_text:             switch_crh6f_cargo_subtype;

        // Graphics
        default:                        switch_crh6f_graphics;
        purchase:                       spriteset_crh6f_purchase_original;
        articulated_part:               switch_crh6f_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        power:                          return 0;
        purchase_power:                 return 1642;
        tractive_effort_coefficient:    return 0;
        purchase_tractive_effort_coefficient: return int(0.2*255);
        cargo_capacity:                 switch_crh6f_head_capacity;
        cargo_age_period:               140 << param_cargo_decay;
        cost_factor:                    65;
        running_cost_factor:            50 * runningcostfactor() + 20;
        purchase_running_cost_factor:   220;
        purchase_cargo_capacity:        216/3;
    }

    // Multiple Unit Wagons
    livery_override(zemuw) {
        default:                        switch_crh6f_wagon_graphics;
        cargo_capacity:                 switch_crh6f_wagon_capacity;
        power:                          switch_crh6f_wagon_power;
        tractive_effort_coefficient:    switch_crh6f_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         48;
        can_attach_wagon:               muhead;
        cargo_age_period:               120 << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh6f_wagon_cost;
        running_cost_factor:            switch_crh6f_wagon_running() * runningcostfactor() + accostfactor();
    }
}

// End CRH6F

