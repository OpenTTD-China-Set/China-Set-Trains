//Begin KD25G
switch (FEAT_TRAINS, SELF, switch_kd25g_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25G_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_kd25g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "kd25g")
}

// Vehicle
spriteset (spriteset_kd25g_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25g")
}
spriteset (spriteset_kd25g_luchaogang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25g-luchaogang")
}
spriteset (spriteset_kd25g_orange, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25g-orange")
}
spriteset (spriteset_kd25g_xihu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25g-xihu")
}
spriteset (spriteset_kd25g_jinwen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25g-jinwen")
}

switch (FEAT_TRAINS, SELF, switch_kd25g_articulated_part, extra_callback_info1) {
    1..2: return kd25g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_kd25g_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_RED_WHITE);
    2: return string(STR_LUCHAOGANG);
    3: return string(STR_ORANGE);
    4: return string(STR_XIHU);
    5: return string(STR_GAOZU_GREEN);
    6: return string(STR_JINWEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_kd25g_build_year_sprite, build_year) {
    spriteset_kd25g_original; // Unfinished
}

switch (FEAT_TRAINS, SELF, switch_kd25g_graphics_subtype, cargo_subtype) {
    0: switch_kd25g_build_year_sprite;
    1: spriteset_kd25g_original;
    2: spriteset_kd25g_luchaogang;
    3: spriteset_kd25g_orange;
    4: spriteset_kd25g_xihu;
    5: spriteset_kd25g_original;  // Will change to "spriteset_kd25g_gaozu" once the "gaozu green" sprite is finished
    6: spriteset_kd25g_jinwen;
}

switch (FEAT_TRAINS, SELF, switch_kd25g_graphics, position_in_articulated_veh % 3) {
    1: switch_kd25g_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, kd25g) {
    property {
        // Menu
        name:                           string(STR_NAME_KD25G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					yz25g;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         60 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_KD25G), string(STR_KD25G_LIVERY_AVAILABILITY), string(STR_AIR_CONDITIONER_EFFECT));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_kd25g_graphics;
        purchase:                       spriteset_kd25g_purchase_original;
        cargo_subtype_text:             switch_kd25g_cargo_subtype;
        articulated_part:               switch_kd25g_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cargo_age_period:               200 << param_cargo_decay;
        cost_factor:                    23;
        running_cost_factor:            5 * runningcostfactor() + 20;
        purchase_running_cost_factor:   40;
        purchase_cargo_capacity:        0;
        name:                            switch_kd25g_name;

    }
}

// End KD25G

