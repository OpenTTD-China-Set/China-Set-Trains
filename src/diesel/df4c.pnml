// Begin DF4C

// Graphics

// Purchase Menu
spriteset (spriteset_df4c_purchase) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_df4c_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df4c")
}

// Vehicle
spriteset (spriteset_df4c_original) {
    template_empty ()
}
spriteset (spriteset_df4c_henanxinzheng) {
    template_empty ()
}
spriteset (spriteset_df4c_ukraine) {
    template_empty ()
}
alternative_sprites(spriteset_df4c_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4c")
}
alternative_sprites(spriteset_df4c_henanxinzheng, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4c-henan-xinzheng")
}
alternative_sprites(spriteset_df4c_ukraine, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df4c-ukraine")
}

switch (FEAT_TRAINS, SELF, switch_df4c_articulated_part, extra_callback_info1) {
    1..2: return df4c;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df4c_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_HENAN_XINZHENG);
    2: return string(STR_UKRAINE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df4c_graphics_subtype, cargo_subtype) {
    0: spriteset_df4c_original;
    1: spriteset_df4c_henanxinzheng;
    2: spriteset_df4c_ukraine;
}

switch (FEAT_TRAINS, SELF, switch_df4c_graphics, position_in_articulated_veh % 3) {
    1: switch_df4c_graphics_subtype;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_df4c_capacity, position_in_articulated_veh % 3) {
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_df4c_additional_text, param_cargo_decay) {
    return string(STR_DESC_5, string(STR_FULL_NAME_DF4C), string(STR_ALIASNAME_DF), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_DF4C_LIVERY_AVAILABILITY), string(STR_DF4C_NICKNAME), string(STR_RELIABILITY_16));
}


item (FEAT_TRAINS, df4c) {
    property {
        // Menu
        name:                           string(STR_NAME_DF4C);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1985,01,01);
        model_life:                     50;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          2648 kW;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.325;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                switch_df4c_additional_text;
        cargo_subtype_text:             switch_df4c_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df4c_graphics;
        purchase:                       spriteset_df4c_purchase;
        articulated_part:               switch_df4c_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    66;
        running_cost_factor:            53 * runningcostfactor();
        purchase_running_cost_factor:   212;
        purchase_cargo_capacity:        0;
    }
}

// End DF4C

