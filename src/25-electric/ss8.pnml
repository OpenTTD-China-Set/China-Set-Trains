// Begin SS8

// Graphics

// Purchase Menu
spriteset (spriteset_ss8_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "ss8")
}

// Vehicle
spriteset (spriteset_ss8_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss8")
}

switch (FEAT_TRAINS, SELF, switch_ss8_articulated_part, extra_callback_info1) {
    1..2: return ss8;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ss8_graphics, position_in_articulated_veh % 3) {
    1: spriteset_ss8_original;
    empty_sprites;
}


item (FEAT_TRAINS, ss8) {
    property {
        // Menu
        name:                           string(STR_NAME_SS8);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1994,07,01);
        model_life:                     50;
        retire_early:                   30;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          170 km/h;
        power:                          3600 kW;
        weight:                         88 ton;
        tractive_effort_coefficient:    0.243;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              10;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SS8), string(STR_ELECTRICITY_SUPPLY_YES), string(STR_SS8_NICKNAME), string(STR_RELIABILITY_10));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_ss8_graphics;
        purchase:                       spriteset_ss8_purchase_original;
        articulated_part:               switch_ss8_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    90;
        running_cost_factor:            72 * runningcostfactor() + 20;
        purchase_running_cost_factor:   308;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           bitmask(2);
    }
}

// End SS8

