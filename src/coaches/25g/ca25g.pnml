// Begin CA25G

// Graphics

// Purchase Menu
spriteset (spriteset_ca25g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "ca25g")
}

// Vehicle
spriteset (spriteset_ca25g_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25g")
}
spriteset (spriteset_ca25g_orange, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25g-orange")
}
spriteset (spriteset_ca25g_jinwen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "ca25g-jinwen")
}

switch (FEAT_TRAINS, SELF, switch_ca25g_articulated_part, extra_callback_info1) {
    1..2: return ca25g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_RED_WHITE);
    2: return string(STR_ORANGE);
    3: return string(STR_GAOZU_GREEN);
    4: return string(STR_JINWEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_build_year_sprite, build_year) {
    spriteset_ca25g_original; // Unfinished
}

switch (FEAT_TRAINS, SELF, switch_ca25g_graphics_subtype, cargo_subtype) {
    0: switch_ca25g_build_year_sprite;
    1: spriteset_ca25g_original;
    2: spriteset_ca25g_orange;
    3: spriteset_ca25g_original;  // Will change to "spriteset_25g_gaozu" once the "gaozu green" sprite is finished
    4: spriteset_ca25g_jinwen;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_graphics, position_in_articulated_veh % 3) {
    1: switch_ca25g_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca25g_capacity, position_in_articulated_veh % 3) {
    1: return 48;
    return 0;
}


item (FEAT_TRAINS, ca25g) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         44.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA25G), string(STR_CAFE_EFFECT), string(STR_CA25G_LIVERY_AVAILABILITY), string(STR_COMFORT_160_CAFE));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca25g_cargo_subtype;

        // Graphics
        default:                        switch_ca25g_graphics;
        purchase:                       spriteset_ca25g_purchase_original;
        articulated_part:               switch_ca25g_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25g_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    25;
        running_cost_factor:            4 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   66;
        purchase_cargo_capacity:        48/3;
    }
}

// End CA25G

