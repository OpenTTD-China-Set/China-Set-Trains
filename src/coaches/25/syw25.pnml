// Begin SYW25
switch (FEAT_TRAINS, SELF, switch_syw25_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_S25_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_syw25_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25","syw25")
}

// Vehicle
spriteset (spriteset_syw25_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25","syw25")
}

switch (FEAT_TRAINS, SELF, switch_syw25_articulated_part, extra_callback_info1) {
    1..2: return syw25;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_syw25_graphics, position_in_articulated_veh % 3) {
    1: spriteset_syw25_original;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_syw25_capacity, position_in_articulated_veh % 3) {
    1: return 80;
    return 0;
}


item (FEAT_TRAINS, syw25) {
    property {
        // Menu
        name:                           string(STR_NAME_SYW25);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					syz25;
 

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2014,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 80;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               333 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         53.6 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }

    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_SYW25), string(STR_AIR_CONDITIONER_NEED_POWER), string(STR_S25_LIVERY_AVAILABILITY), string(STR_COMFORT_HIGH, 288));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_syw25_graphics;
        purchase:                       spriteset_syw25_purchase_original;
        articulated_part:               switch_syw25_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_syw25_capacity;
        cargo_age_period:               (18 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    13;
        running_cost_factor:            11 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        80/3;
        name:                            switch_syw25_name;

    }
}

// End SYW25
