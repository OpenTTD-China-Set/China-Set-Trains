// Begin CRH380A
switch (FEAT_TRAINS, SELF, switch_crh380a_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_CRH380A_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_crh380a_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "crh380a-tc")
}

// Vehicle
spriteset (spriteset_crh380a_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh380a-tc")
}
spriteset (spriteset_crh380a_middle_m, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh380a-t")
}
spriteset (spriteset_crh380a_middle_p, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh380a-tp")
}
spriteset (spriteset_crh380a_middle_p_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh380a-tp")
}
spriteset (spriteset_crh380a_cab, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh380a-tc")
}
spriteset (spriteset_crh380a_cab_reconect, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh380a-tc-c")
}
spriteset (spriteset_crh380a_cab_reconect_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh380a-tc-c")
}


switch (FEAT_TRAINS, SELF, switch_crh380a_articulated_part, extra_callback_info1) {
    1..2: return crh380a;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_front_graphics, position_in_articulated_veh % 3) {
    1: spriteset_crh380a_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_head_capacity, position_in_articulated_veh % 3) {
    1: return 32;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_three_quarters, ((position_in_consist / 3) + 1) == (num_vehs_in_consist * 3 / 12)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_p_reversed;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_one_quarter, ((position_in_consist / 3) + 1) == (num_vehs_in_consist / 12)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_p_reversed;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_in_witch_quarters, ((position_in_consist / 3) + 1) == (num_vehs_in_consist * 3 / 12) || ((position_in_consist / 3) == (num_vehs_in_consist *3 / 12))) {
    1: switch_crh380a_middle_wagon_three_quarters;
    switch_crh380a_middle_wagon_one_quarter;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_if_middle_wagon_in_quarters, (((position_in_consist / 3) + 1) == (num_vehs_in_consist * 3 / 12) || ((position_in_consist / 3) == (num_vehs_in_consist *3 / 12))) || (((position_in_consist / 3) + 1) == (num_vehs_in_consist / 12) || ((position_in_consist / 3) == (num_vehs_in_consist / 12)))) {
    1: switch_crh380a_middle_wagon_in_witch_quarters;
    spriteset_crh380a_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_half_long, ((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) {
    1: spriteset_crh380a_cab_reconect;
    spriteset_crh380a_cab_reconect_reversed;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_reconect, (((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) || ((position_in_consist / 3) == (num_vehs_in_consist / 6))) {
    1: switch_crh380a_middle_wagon_half_long;
    switch_crh380a_if_middle_wagon_in_quarters;
}


switch (FEAT_TRAINS, SELF, switch_crh380a_middle_wagon_half_meddium, ((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_p_reversed;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_if_middle_wagon_half_meddium, (((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) || ((position_in_consist / 3) == (num_vehs_in_consist / 6))) {
    1: switch_crh380a_middle_wagon_half_meddium;
    spriteset_crh380a_middle_m;
}


switch (FEAT_TRAINS, SELF, switch_crh380a_if_middle_wagon_half_short, ((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_m;
}


switch (FEAT_TRAINS, SELF, switch_crh380a_length_class, num_vehs_in_consist / 3) {
    2..6:   switch_crh380a_if_middle_wagon_half_short;
    7..12:  switch_crh380a_if_middle_wagon_half_meddium;
    switch_crh380a_middle_wagon_reconect;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_if_wagon_end, position_in_consist_from_end == 1) {
    1: spriteset_crh380a_cab;
    switch_crh380a_length_class;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_graphics, (position_in_consist_from_end % 3) == 1) {
    1: switch_crh380a_if_wagon_end;
    empty_sprites;
}

/* switch (FEAT_TRAINS, SELF, switch_crh380a_short_graphics, (position_in_consist_from_end == position_in_consist) || (position_in_consist_from_end == position_in_consist + 3)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_long_graphics, (position_in_consist_from_end == position_in_consist + 12) || (position_in_consist_from_end == position_in_consist + 9) || (position_in_consist_from_end + 9 == position_in_consist) || (position_in_consist_from_end + 12 == position_in_consist)) {
    1: spriteset_crh380a_middle_p;
    spriteset_crh380a_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_graphics_1, num_vehs_in_consist) {
    0..24: switch_crh380a_short_graphics;
    switch_crh380a_long_graphics;
}

 switch (FEAT_TRAINS, SELF, switch_crh380a_middle_graphics, position_in_articulated_veh % 3) {
    1: switch_crh380a_middle_graphics_1;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_graphics, position_in_articulated_veh % 3) {
    1: spriteset_crh380a_cab;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_graphics, position_in_consist_from_end) {
    0..2: switch_crh380a_cab_graphics;
    switch_crh380a_middle_graphics;
} */

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity_ze, position_in_articulated_veh % 3) {
    1: return 85;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity_zy, position_in_articulated_veh % 3) {
    1: return 56;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity_zs, position_in_articulated_veh % 3) {
    1: return 24;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity_sw, position_in_articulated_veh % 3) {
    1: return 15;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity_zec, position_in_articulated_veh % 3) {
    1: return 63;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_middle_capacity, vehicle_type_id) {
    zemuw: switch_crh380a_middle_capacity_ze;
    zymuw: switch_crh380a_middle_capacity_zy;
    zsmuw: switch_crh380a_middle_capacity_zs;
    swmuw: switch_crh380a_middle_capacity_sw;
    zecmuw: switch_crh380a_middle_capacity_zec;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity_ze, position_in_articulated_veh % 3) {
    1: return 50;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity_zy, position_in_articulated_veh % 3) {
    1: return 32;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity_zs, position_in_articulated_veh % 3) {
    1: return 12;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity_sw, position_in_articulated_veh % 3) {
    1: return 9;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity_zec, position_in_articulated_veh % 3) {
    1: return 36;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_cab_capacity, vehicle_type_id) {
    zemuw: switch_crh380a_cab_capacity_ze;
    zymuw: switch_crh380a_cab_capacity_zy;
    zsmuw: switch_crh380a_cab_capacity_zs;
    swmuw: switch_crh380a_cab_capacity_sw;
    zecmuw: switch_crh380a_cab_capacity_zec;
    return 0;
}


switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_capacity_if_reconect_cab, (((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) || ((position_in_consist / 3) == (num_vehs_in_consist / 6))) {
    0: switch_crh380a_middle_capacity;
    switch_crh380a_cab_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_capacity_reconect, (position_in_consist < 3) || (position_in_consist_from_end < 3)) {
    0: switch_crh380a_wagon_capacity_if_reconect_cab;
    switch_crh380a_cab_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_capacity_single, (position_in_consist < 3) || (position_in_consist_from_end < 3)) {
    0: switch_crh380a_middle_capacity;
    switch_crh380a_cab_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_capacity, num_vehs_in_consist / 3) {
    2..12: switch_crh380a_wagon_capacity_single;
    switch_crh380a_wagon_capacity_reconect;
}

/* switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_capacity, position_in_consist_from_end) {
    0..2: switch_crh380a_cab_capacity;
    switch_crh380a_middle_capacity;
} */

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_cost_power, vehicle_type_id) {
    zemuw: return 66;
    zymuw: return 69;
    zsmuw: return 73;
    swmuw: return 74;
    zecmuw: return 85;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_running_power, vehicle_type_id) {
    zemuw: return 54;
    zymuw: return 67;
    zsmuw: return 61;
    swmuw: return 62;
    zecmuw: return 63;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_cost_unpower, vehicle_type_id) {
    zemuw: return 20;
    zymuw: return 24;
    zsmuw: return 28;
    swmuw: return 29;
    zecmuw: return 39;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_running_unpower, vehicle_type_id) {
    zemuw: return 8;
    zymuw: return 12;
    zsmuw: return 16;
    swmuw: return 17;
    zecmuw: return 12;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_cost, vehicle_is_potentially_powered) {
    1: switch_crh380a_wagon_cost_power;
    switch_crh380a_wagon_cost_unpower;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_running, vehicle_is_potentially_powered) {
    1: switch_crh380a_wagon_running_power;
    switch_crh380a_wagon_running_unpower;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_power_if_reconect_cab, (((position_in_consist / 3) + 1) == (num_vehs_in_consist / 6)) || ((position_in_consist / 3) == (num_vehs_in_consist / 6))) {
    0: return 2038; // 2038 HP = 1520 KW
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_power_reconect, (position_in_consist < 3) || (position_in_consist_from_end < 3)) {
    0: switch_crh380a_wagon_power_if_reconect_cab;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_power_single, (position_in_consist < 3) || (position_in_consist_from_end < 3)) {
    0: return 2038; // 2038 HP = 1520 KW
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_power, num_vehs_in_consist / 3) {
    2..12: switch_crh380a_wagon_power_single;
    switch_crh380a_wagon_power_reconect;
}

/* switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_power, (position_in_consist < 3) || (position_in_consist_from_end < 3)) {
    0: return 2038; // 2038 HP = 1520 KW
    return 0;
} */

switch (FEAT_TRAINS, SELF, switch_crh380a_wagon_traction, vehicle_is_potentially_powered) {
    1: return int(0.0825*255);
    return 0;
}


item (FEAT_TRAINS, crh380a) {
    property {
        // Menu
        name:                           string(STR_NAME_CRH380A);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2010,09,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   25;
        track_type:                     SAAA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               400 << param_cargo_decay;

        // Stats
        speed:                          310 km/h;
        power:                          1520 kW;
        weight:                         51 ton;
        tractive_effort_coefficient:    0.083;
        cost_factor:                    68;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            56;
        reliability_decay:              12;
        bitmask_vehicle_info:           bitmask(2);
        
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_6, string(STR_CRH_CAN_ATTACH_WAGON), string(STR_CRH380A_NICKNAME), string(STR_RELIABILITY_12), string(STR_CRH380A_CONSIST), string(STR_CRH380A_HEAD_SEAT), string(STR_350_380_AVAILABLE));
        can_attach_wagon:               crhwagon;
        start_stop:                     mu4to24car;
        cargo_subtype_text:             switch_crh380_cargo_subtype;

        // Graphics
        default:                        switch_crh380a_front_graphics;
        purchase:                       spriteset_crh380a_purchase_original;
        articulated_part:               switch_crh380a_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        speed:                          switch_crh380_speed;
        power:                          return 0;
        purchase_power:                 return 2038; // HP
        tractive_effort_coefficient:    return 0;
        purchase_tractive_effort_coefficient: return int(0.083*255);
        cargo_capacity:                 switch_crh380a_head_capacity;
        cargo_age_period:               12 * 5 * cafedecayfactor() << param_cargo_decay;
        cost_factor:                    68;
        running_cost_factor:            (54 + crhspeedcost()) * runningcostfactor() * cafecostfactor() + 20;
        purchase_running_cost_factor:   236;
        purchase_cargo_capacity:        36/3;
        name:                           switch_crh380a_name;
    }

    // Multiple Unit Wagons
    livery_override(zemuw) {
        default:                        switch_crh380a_wagon_graphics;
        cargo_capacity:                 switch_crh380a_wagon_capacity;
        power:                          switch_crh380a_wagon_power;
        tractive_effort_coefficient:    switch_crh380a_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh380a_wagon_cost;
        running_cost_factor:            (switch_crh380a_wagon_running() + crhspeedcost()) * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(zymuw) {
        default:                        switch_crh380a_wagon_graphics;
        cargo_capacity:                 switch_crh380a_wagon_capacity;
        power:                          switch_crh380a_wagon_power;
        tractive_effort_coefficient:    switch_crh380a_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh380a_wagon_cost;
        running_cost_factor:            (switch_crh380a_wagon_running() + crhspeedcost()) * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(zsmuw) {
        default:                        switch_crh380a_wagon_graphics;
        cargo_capacity:                 switch_crh380a_wagon_capacity;
        power:                          switch_crh380a_wagon_power;
        tractive_effort_coefficient:    switch_crh380a_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh380a_wagon_cost;
        running_cost_factor:            (switch_crh380a_wagon_running() + crhspeedcost()) * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(swmuw) {
        default:                        switch_crh380a_wagon_graphics;
        cargo_capacity:                 switch_crh380a_wagon_capacity;
        power:                          switch_crh380a_wagon_power;
        tractive_effort_coefficient:    switch_crh380a_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh380a_wagon_cost;
        running_cost_factor:            (switch_crh380a_wagon_running() + crhspeedcost()) * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(zecmuw) {
        default:                        switch_crh380a_wagon_graphics;
        cargo_capacity:                 switch_crh380a_wagon_capacity;
        power:                          switch_crh380a_wagon_power;
        tractive_effort_coefficient:    switch_crh380a_wagon_traction;
        length:                         switch_length_2_8_2;
        weight:                         51;
        can_attach_wagon:               muhead;
        cargo_age_period:               (switch_crh_wagon_cargo_decay() * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh380a_wagon_cost;
        running_cost_factor:            (switch_crh380a_wagon_running() + crhspeedcost()) * runningcostfactor() + 50 + accostfactor(); // IT IS A RESTAURANT CAR!
    }
}

// End CRH380A

