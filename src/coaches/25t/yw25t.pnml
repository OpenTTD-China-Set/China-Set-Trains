//Begin YW25T
switch (FEAT_TRAINS, SELF, switch_yw25t_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25T_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// // Purchase Menu
spriteset (spriteset_yw25t_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25t/yw25t")
}

// Vehicle
spriteset (spriteset_yw25t_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yw25t")
}
spriteset (spriteset_yW25t_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yw25t-green")
}
spriteset (spriteset_yw25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yw25t-xz")
}
spriteset (spriteset_bspyw25t_qingzang, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/bspyw25t-xz")
}
spriteset (spriteset_yw25t_int_dprk, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25t/yw25t-int-dprk")
}


switch (FEAT_TRAINS, SELF, switch_yw25t_articulated_part, extra_callback_info1) {
    1..2: return yw25t;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_yw25t_cargo_subtype, cargo_subtype) {
    0: string(STR_BUILD_YEAR);
    1: string(STR_ORIGINAL);
    2: string(STR_GAOZU_GREEN);
    3: string(STR_QINGZANG);
    4: string(STR_QINGZANG_BSP);
    5: string(STR_INTERNATIONAL_DPRK);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_yw25t_graphics_subtype_year_depend, build_year) {
    0..2014: spriteset_yw25t_original;
    spriteset_yW25t_green;
}
switch (FEAT_TRAINS, SELF, switch_yw25t_graphics_subtype_0, param_gaozulv) {
    1: switch_yw25t_graphics_subtype_year_depend;
    spriteset_yw25t_original;
}

switch (FEAT_TRAINS, SELF, switch_yw25t_graphics_subtype, cargo_subtype) {
    0: switch_yw25t_graphics_subtype_0;
    1: spriteset_yw25t_original;
    2: spriteset_yW25t_green;
    3: spriteset_yw25t_qingzang;
    4: spriteset_bspyw25t_qingzang;
    5: spriteset_yw25t_int_dprk;
}

switch (FEAT_TRAINS, SELF, switch_yw25t_graphics, position_in_articulated_veh % 3) {
    1: switch_yw25t_graphics_subtype;
    empty_sprites;
}


switch (FEAT_TRAINS, SELF, switch_yw25t_capacity, position_in_articulated_veh % 3) {
    1: return 66;
    return 0;
}


item (FEAT_TRAINS, yw25t) {
    property {
        // Menu
        name:                           string(STR_NAME_YW25T);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2052;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2004,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 66;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               370 << param_cargo_decay;

        // Stats
        speed:                          160 km/h;
        power:                          0 hp;
        weight:                         52.9 ton;
        cost_factor:                    13;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            10;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_YW25T), string(STR_YZ_YW25T_LIVERY_AVAILABILITY), string(STR_COMFORT_HIGH, 288));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_yw25t_cargo_subtype;

        // Graphics
        default:                        switch_yw25t_graphics;
        purchase:                       spriteset_yw25t_purchase_original;
        articulated_part:               switch_yw25t_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_yw25t_capacity;
        cargo_age_period:               (18 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    12;
        running_cost_factor:            12 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   48;
        purchase_cargo_capacity:        66/3;
        name:                           switch_yw25t_name;

    }
}

// End YW25T

