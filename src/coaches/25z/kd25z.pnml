// Begin KD25Z

// Graphics

// Purchase Menu
spriteset (spriteset_kd25z_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "kd25z")
}

// Vehicle
spriteset (spriteset_kd25z_xizi, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25z")
}
spriteset (spriteset_kd25z_beijing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25z-beijing")
}
spriteset (spriteset_kd25z_guangzhou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kd25z-guangzhou")
}

switch (FEAT_TRAINS, SELF, switch_kd25z_articulated_part, extra_callback_info1) {
    1..2: return kd25z;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_kd25z_cargo_subtype, cargo_subtype) {
    0: return string(STR_XIZI);
    1: return string(STR_BEIJING);
    2: return string(STR_GUANGSHEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_kd25z_graphics_subtype, cargo_subtype) {
    0: spriteset_kd25z_xizi;
    1: spriteset_kd25z_beijing;
    2: spriteset_kd25z_guangzhou;
}

switch (FEAT_TRAINS, SELF, switch_kd25z_graphics, position_in_articulated_veh % 3) {
    1: switch_kd25z_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, kd25z) {
    property {
        // Menu
        name:                           string(STR_NAME_KD25Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         59 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_KD25Z), string(STR_25Z_LIVERY_AVAILABILITY), string(STR_AIR_CONDITIONER_EFFECT));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_kd25z_graphics;
        purchase:                       spriteset_kd25z_purchase_original;
        cargo_subtype_text:             switch_kd25z_cargo_subtype;
        articulated_part:               switch_kd25z_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cargo_age_period:               200 << param_cargo_decay;
        cost_factor:                    24;
        running_cost_factor:            6 * runningcostfactor() + 20;
        purchase_running_cost_factor:   44;
        purchase_cargo_capacity:        0;
    }
}

// End KD25Z

