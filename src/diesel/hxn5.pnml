// Begin HXN5
switch (FEAT_TRAINS, SELF, switch_hxn5_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_HXN5_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_hxn5_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "hxn5")
}

// Vehicle
spriteset (spriteset_hxn5_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn5")
}

spriteset (spriteset_hxn5_reverse, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "hxn5-reverse")
}

switch (FEAT_TRAINS, SELF, switch_hxn5_articulated_part, extra_callback_info1) {
    1..2: return hxn5;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxn5_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_DESC_2, string(STR_REVERSE), string(STR_ORIGINAL));
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_hxn5_graphics_subtype, cargo_subtype) {
    0: spriteset_hxn5_original;
    1: spriteset_hxn5_reverse;
}

switch (FEAT_TRAINS, SELF, switch_hxn5_graphics, position_in_articulated_veh % 3) {
    1: switch_hxn5_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, hxn5) {
    property {
        // Menu
        name:                           string(STR_NAME_HXN5);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2008,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          4660 kW;
        weight:                         150 ton;
        tractive_effort_coefficient:    0.422;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              8;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_HXN5), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_HXN5_NICKNAME), string(STR_RELIABILITY, 8));
        cargo_subtype_text:             switch_hxn5_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_hxn5_graphics;
        purchase:                       spriteset_hxn5_purchase;
        articulated_part:               switch_hxn5_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cost_factor:                    98;
        running_cost_factor:            83 * runningcostfactor();
        purchase_running_cost_factor:   332;
        purchase_cargo_capacity:        0;
        name:                           switch_hxn5_name;
    }
}

// End HXN5

