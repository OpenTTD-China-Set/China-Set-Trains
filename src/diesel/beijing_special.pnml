//Begin BEIJING_SPECIAL
switch (FEAT_TRAINS, SELF, switch_beijing_special_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BEIJING_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_beijing_special_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel/beijing", "beijing-special")
}

//Adds Vehicle
spriteset (spriteset_beijing_special, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel/beijing", "beijing-special")
}
spriteset (spriteset_beijing_special_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed_9 ("diesel/beijing", "beijing-special")
}

switch (FEAT_TRAINS, SELF, switch_beijing_special_articulated_part, extra_callback_info1) {
    1..5: return beijing_special;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_beijing_special_graphics, position_in_articulated_veh % 6) {
    1: spriteset_beijing_special;
    4: spriteset_beijing_special_reversed;
    empty_sprites;
}


item (FEAT_TRAINS, beijing_special) {
    property {
        // Menu
        name:                           string(STR_NAME_BEIJING_SPECIAL);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        variant_group:					beijing_freight;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1986,06,01);
        model_life:                     60;
        retire_early:                   40;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          5400 hpM;
        weight:                         184 ton;
        tractive_effort_coefficient:    0.272;
        cost_factor:                    1;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            1;
        reliability_decay:              3;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_BEIJING_SPECIAL), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_BEIJING_SPECIAL_NICKNAME), string(STR_RELDEC_VERY_LOW, 3));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_beijing_special_graphics;
        purchase:                       spriteset_beijing_special_purchase;
        articulated_part:               switch_beijing_special_articulated_part;

        // Stats
        length:                         switch_length_2_5_2;
        cargo_capacity:                 0;
        cost_factor:                    110;
        running_cost_factor:            85 * runningcostfactor();
        purchase_running_cost_factor:   340;
        purchase_cargo_capacity:        0;
        name:                           switch_beijing_special_name;

    }
}

// End BEIJING_SPECIAL