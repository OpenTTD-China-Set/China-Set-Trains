//Begin HXD11000
switch (FEAT_TRAINS, SELF, switch_hxd11000_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_HXD1_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_hxd11000_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "hxd11000-front")
}

// Vehicle
spriteset (spriteset_hxd11000_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "hxd11000-front")
}
spriteset (spriteset_hxd11000_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "hxd11000-back")
}

switch (FEAT_TRAINS, SELF, switch_hxd11000_articulated_part, extra_callback_info1) {
    1..5: return hxd11000;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxd11000_graphics, position_in_articulated_veh % 6) {
    1: spriteset_hxd11000_front;
    4: spriteset_hxd11000_back;
    empty_sprites;
}


item (FEAT_TRAINS, hxd11000) {
    property {
        // Menu
        name:                           string(STR_NAME_HXD11000);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					6149;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2012,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          9600 kW;
        weight:                         184 ton;
        tractive_effort_coefficient:    0.421;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              8;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_HXD11000), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_8));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_hxd11000_graphics;
        purchase:                       spriteset_hxd11000_purchase_original;
        articulated_part:               switch_hxd11000_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    146;
        running_cost_factor:            118 * runningcostfactor();
        purchase_running_cost_factor:   472;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           0;
        name:                           switch_hxd11000_name;

    }
}

// End HXD11000

