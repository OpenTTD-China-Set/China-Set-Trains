// Begin X2000

// Graphics

// Purchase Menu
spriteset (spriteset_x2000_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "x2000-m")
}

// Vehicle
spriteset (spriteset_x2000_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "x2000-m")
}
spriteset (spriteset_x2000_middle, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "x2000-t")
}
spriteset (spriteset_x2000_cab, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "x2000-tc")
}

switch (FEAT_TRAINS, SELF, switch_x2000_articulated_part, extra_callback_info1) {
    1..2: return x2000;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}



switch (FEAT_TRAINS, SELF, switch_x2000_graphics, position_in_articulated_veh % 3) {
    1: spriteset_x2000_front;
    empty_sprites;
}


switch (FEAT_TRAINS, SELF, switch_x2000_cab_graphics, (position_in_consist_from_end / 3) + 1) {
    1: spriteset_x2000_cab;
    spriteset_x2000_middle;
}

switch (FEAT_TRAINS, SELF, switch_x2000_wagon_graphics, position_in_articulated_veh % 3) {
    1: switch_x2000_cab_graphics;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_x2000_wagon_length, (position_in_consist_from_end / 3) + 1) {
    1: switch_length_2_6_2;
    switch_length_2_8_2;
}

//cap

switch (FEAT_TRAINS, SELF, switch_x2000_middle_capacity, position_in_articulated_veh % 3) {
    1: return 76;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_x2000_cab_capacity, position_in_articulated_veh % 3) {
    1: return 50;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_x2000_wagon_capacity, position_in_consist_from_end) {
    0..2: switch_x2000_cab_capacity;
    switch_x2000_middle_capacity;
}



item (FEAT_TRAINS, x2000) {
    property {
        // Menu
        name:                           string(STR_NAME_X2000);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1998,04,10);
        model_life:                     30;
        retire_early:                   20;
        vehicle_life:                   15;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          200 km/h;
        power:                          4800 kW;
        weight:                         73 ton;
        tractive_effort_coefficient:    0.23;
        cost_factor:                    96;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            44;
        reliability_decay:              10;
        bitmask_vehicle_info:           bitmask(2);
        curve_speed_mod:                0.2;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_X2000_CAN_ATTACH_WAGON), string(STR_X2000_CONSIST), string(STR_SE_IMPORT), string(STR_FIT_DESC),string(STR_RELDEC_LOW, 10));
        can_attach_wagon:               onlyzy;
        start_stop:                     mu4to12car;

        // Graphics
        default:                        switch_x2000_graphics;
        purchase:                       spriteset_x2000_purchase_original;
        articulated_part:               switch_x2000_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    84;
        running_cost_factor:            69 * runningcostfactor() + 20;
        purchase_running_cost_factor:   296;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           switch_pushpull_consist_correction;
    }

    // Multiple Unit Wagons
    livery_override(zymuw) {
        default:                        switch_x2000_wagon_graphics;
        cargo_capacity:                 switch_x2000_wagon_capacity;
        length:                         switch_x2000_wagon_length;
        weight:                         47;
        can_attach_wagon:               muhead;
        cargo_age_period:               (400) << param_cargo_decay;
        bitmask_vehicle_info:           0;
        articulated_part:               switch_zymuw_articulated_part;
        cost_factor:                    9;
        running_cost_factor:            8 * runningcostfactor() * 2 + 2; // NO CAFE, WITH AC
    }
}

// End X2000
