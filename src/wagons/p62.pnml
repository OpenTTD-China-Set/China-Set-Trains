//Begin P62
switch (FEAT_TRAINS, SELF, switch_p62_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_BOXCAR);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_p62_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p62")
}

// Vehicle
spriteset (spriteset_p62_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62")
}
spriteset (spriteset_p62_blue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-blue")
}
spriteset (spriteset_p62_damaged_severe, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-damaged-severe")
}
spriteset (spriteset_p62_lightblack, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-lightblack")
}
spriteset (spriteset_p62_lightblack_damaged, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-lightblack-damaged")
}
spriteset (spriteset_p62_rustyred, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-rustyred")
}
spriteset (spriteset_p62_rustyred_damaged, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-rustyred-damaged")
}
spriteset (spriteset_p62_brown, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p62-brown")
}

/* random_switch (FEAT_TRAINS, SELF, random_p62_colour) {
    1: spriteset_p62_black; // More colours waiting to be added
    1: spriteset_p62_blue;
    1: spriteset_p62_damaged_severe;
    1: spriteset_p62_lightblack;
    1: spriteset_p62_lightblack_damaged;
    1: spriteset_p62_rustyred;
    1: spriteset_p62_rustyred_damaged;
    1: spriteset_p62_brown;
} */
random_switch (FEAT_TRAINS, SELF, random_p62_colour_early) {
    20: spriteset_p62_black; // More colours waiting to be added
    6: spriteset_p62_blue;
    1: spriteset_p62_damaged_severe;
    12: spriteset_p62_lightblack;
    1: spriteset_p62_lightblack_damaged;
    1: spriteset_p62_rustyred;
    1: spriteset_p62_rustyred_damaged;
    12: spriteset_p62_brown;
}
random_switch (FEAT_TRAINS, SELF, random_p62_colour_middium) {
    20: spriteset_p62_black; // More colours waiting to be added
    4: spriteset_p62_blue;
    4: spriteset_p62_damaged_severe;
    15: spriteset_p62_lightblack;
    5: spriteset_p62_lightblack_damaged;
    15: spriteset_p62_rustyred;
    2: spriteset_p62_rustyred_damaged;
    15: spriteset_p62_brown;
}
random_switch (FEAT_TRAINS, SELF, random_p62_colour_late) {
    10: spriteset_p62_black; // More colours waiting to be added
    1: spriteset_p62_blue;
    5: spriteset_p62_damaged_severe;
    10: spriteset_p62_lightblack;
    5: spriteset_p62_lightblack_damaged;
    10: spriteset_p62_rustyred;
    5: spriteset_p62_rustyred_damaged;
    10: spriteset_p62_brown;
}
switch (FEAT_TRAINS, SELF, random_p62_colour, current_year - 1979) {
    0..15: random_p62_colour_early; 
    16..30: random_p62_colour_middium;
    random_p62_colour_late;
}


switch (FEAT_TRAINS, SELF, switch_p62_articulated_part, extra_callback_info1) {
    1..2: return p62;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p62_graphics, position_in_articulated_veh % 3) {
    1: random_p62_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_p62_capacity_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 192;
    return 60;
}

switch (FEAT_TRAINS, SELF, switch_p62_capacity_cargo, cargo_classes == bitmask(CC_MAIL)) {
    1: return 120;
    switch_p62_capacity_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p62_capacity, position_in_articulated_veh % 3) {
    1: switch_p62_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p62_loading_speed_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 12;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p62_loading_speed, cargo_classes == bitmask(CC_MAIL)) {
    1: return 8;
    switch_p62_loading_speed_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p62_decay_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 80;
    return 185;
}

switch (FEAT_TRAINS, SELF, switch_p62_decay, cargo_type_in_veh == LVST) {
    1: return 560;
    switch_p62_decay_pengdaike;
}


item (FEAT_TRAINS, p62) {
    property {
        // Menu
        name:                           string(STR_NAME_P62);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3098;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1980,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_EXPRESS, CC_PIECE_GOODS, CC_COVERED);
        non_refittable_cargo_classes:   bitmask(CC_LIQUID, CC_HAZARDOUS, CC_OVERSIZED);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [TOUR];
        refit_cost:                     0;
        default_cargo_type:             GOOD;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         24 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_P62_LIVERY_AVAILABILITY), string(STR_DECAY_BOXCAR, 185, 80, 560), string(STR_DESC_PENGDAIKE));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_p62_graphics;
        purchase:                       spriteset_p62_purchase_original;
        articulated_part:               switch_p62_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 switch_p62_capacity;
        loading_speed:                  switch_p62_loading_speed() << param_loading_speed;
        cargo_age_period:               switch_p62_decay() << param_cargo_decay;
        cost_factor:                    5;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        60/3;
        name:                            switch_p62_name;

    }
}

// End P62

