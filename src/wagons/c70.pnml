switch (FEAT_TRAINS, SELF, switch_c70_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_C64_FAMILY);
    return CB_RESULT_NO_TEXT;
}
// Begin C70

// Graphics

// Purchase Menu
spriteset (spriteset_c70_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "c70")
}

// Vehicle
spriteset (spriteset_c70_black_empty, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70")
}
spriteset (spriteset_c70_black_coal_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70-coal-half")
}
spriteset (spriteset_c70_black_coal_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70-coal-full")
}
spriteset (spriteset_c70_black_container_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70-container-half")
}
spriteset (spriteset_c70_black_container_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70-container-full")
}
spriteset (spriteset_c70_black_cover, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "c70-cover")
}

switch (FEAT_TRAINS, SELF, switch_c70_black_coal, cargo_count < 45) {
    0: spriteset_c70_black_coal_full;
    spriteset_c70_black_coal_half;
}

switch (FEAT_TRAINS, SELF, switch_c70_black_container, cargo_count < 40) {
    0: spriteset_c70_black_container_full;
    spriteset_c70_black_container_half;
}

switch (FEAT_TRAINS, SELF, switch_c70_cargo, cargo_classes) {
    bitmask(CC_MAIL): switch_c70_black_container;
    bitmask(CC_EXPRESS): switch_c70_black_container;
    bitmask(CC_COVERED): spriteset_c70_black_cover;
    bitmask(CC_BULK): switch_c70_black_coal;
    spriteset_c70_black_cover;
}

switch (FEAT_TRAINS, SELF, switch_c70_black, cargo_count < 5) {
    0: switch_c70_cargo;
    spriteset_c70_black_empty;
}

random_switch (FEAT_TRAINS, SELF, random_c70_colour) {
    1: switch_c70_black; // More colours waiting to be added
}

switch (FEAT_TRAINS, SELF, switch_c70_articulated_part, extra_callback_info1) {
    1..2: return c70;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_c70_graphics, position_in_articulated_veh % 3) {
    1: random_c70_colour;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_c70_capacity_cargo, cargo_type_in_veh) {
    MAIL: return 140;
    GOOD: return 105;
    return 70;
}

switch (FEAT_TRAINS, SELF, switch_c70_capacity, position_in_articulated_veh % 3) {
    1: switch_c70_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_c70_loading_speed, cargo_type_in_veh) {
    MAIL: return 12;
    GOOD: return 12;
    return 8;
}


item (FEAT_TRAINS, c70) {
    property {
        // Menu
        name:                           string(STR_NAME_C70);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3076;


        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2005,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_EXPRESS, CC_BULK, CC_PIECE_GOODS, CC_COVERED, CC_OVERSIZED);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_LIQUID, CC_HAZARDOUS);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [YETI, YETY];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 70;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         23.8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_C70_LIVERY_AVAILABILITY), string(STR_DECAY_185));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_c70_graphics;
        purchase:                       spriteset_c70_purchase;
        articulated_part:               switch_c70_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_c70_capacity;
        loading_speed:                  switch_c70_loading_speed() << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;
        cost_factor:                    6;
        running_cost_factor:            5 * runningcostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        70/3;
        name: switch_c70_name;

    }
}

// End C70

