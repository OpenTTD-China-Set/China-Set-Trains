// Begin HXD1F

// Graphics

// Purchase Menu
spriteset (spriteset_hxd1f_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "hxd1f-front")
}

// Vehicle
spriteset (spriteset_hxd1f_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "hxd1f-front")
}
spriteset (spriteset_hxd1f_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "hxd1f-back")
}
spriteset (spriteset_fxd1b_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "fxd1b-front")
}
spriteset (spriteset_fxd1b_back, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "fxd1b-back")
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_articulated_part, extra_callback_info1) {
    1..5: return hxd1f;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_livery_graphics, position_in_articulated_veh % 6) {
    1: spriteset_hxd1f_front;
    4: spriteset_hxd1f_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_fxd1b_livery_graphics, position_in_articulated_veh % 6) {
    1: spriteset_fxd1b_front;
    4: spriteset_fxd1b_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_graphics_subtype_0, build_year) {
    0..2017: switch_hxd1f_livery_graphics;
    switch_fxd1b_livery_graphics;
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_graphics_subtype, cargo_subtype) {
    0: switch_hxd1f_graphics_subtype_0;
    1: switch_hxd1f_livery_graphics;
}

switch (FEAT_TRAINS, SELF, switch_hxd1f_graphics, position_in_articulated_veh % 3) {
    1: switch_hxd1f_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, hxd1f) {
    property {
        // Menu
        name:                           string(STR_NAME_FXD1B);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2013,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          9600 kW;
        weight:                         240 ton;
        tractive_effort_coefficient:    0.387;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              8;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_FXD1B), string(STR_ALIASNAME_FXD1B), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_8));
        cargo_subtype_text:             switch_hxd1f_cargo_subtype;
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_hxd1f_graphics;
        purchase:                       spriteset_hxd1f_purchase_original;
        articulated_part:               switch_hxd1f_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    136;
        running_cost_factor:            110 * runningcostfactor();
        purchase_running_cost_factor:   440;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           0;
    }
}

// End HXD1F

