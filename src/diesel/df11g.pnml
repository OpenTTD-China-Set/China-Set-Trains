// Begin DF11G

// Graphics

// Purchase Menu
spriteset (spriteset_df11g_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df11g")
}


// Vehicle
spriteset (spriteset_df11g_leaping, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df11g")
}
spriteset (spriteset_df11g_leaping_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed_10 ("diesel", "df11g")
}
spriteset (spriteset_df11g, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df11gb")
}
spriteset (spriteset_df11g_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed_10 ("diesel", "df11gb")
}

switch (FEAT_TRAINS, SELF, switch_df11g_articulated_part, extra_callback_info1) {
    1..2: return df11g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df11g_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_LATER);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df11g_standard_graphics_subtype, cargo_subtype) {
    0: spriteset_df11g_leaping;
    1: spriteset_df11g;
}

switch (FEAT_TRAINS, SELF, switch_df11g_reversed_graphics_subtype, cargo_subtype) {
    0: spriteset_df11g_leaping_reversed;
    1: spriteset_df11g_reversed;
}

// "Splitting Pig"

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_joint, position_in_vehid_chain % 6) {
    1: switch_df11g_standard_graphics_subtype;
    4: switch_df11g_reversed_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_front, position_in_articulated_veh % 3) {
    1: switch_df11g_standard_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_split, position_in_consist_from_end) {
    1: switch_df11g_reversed_graphics_subtype;
    switch_df11g_graphics_front;
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics, num_vehs_in_vehid_chain % 6) {
    0: switch_df11g_graphics_joint;
    3: switch_df11g_graphics_split;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df11g_speed, num_vehs_in_vehid_chain % 6) {
    0: return 170;
    return 120;
}

switch (FEAT_TRAINS, SELF, switch_df11g_power_split, position_in_articulated_veh) {
    0..2: return 4057;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_df11g_power, num_vehs_in_vehid_chain > 3) {
    1: return 4057;
    switch_df11g_power_split;
}


/* switch (FEAT_TRAINS, SELF, switch_df11g_bitmask_info_0, position_in_consist_from_end) {
    0..2: return bitmask(2);
    return 0;
}
switch (FEAT_TRAINS, SELF, switch_df11g_bitmask_info, position_in_vehid_chain % 6) {
    3..5: return bitmask(2);
    switch_df11g_bitmask_info_0;
} */

switch (FEAT_TRAINS, SELF, switch_df11g_traction_split, vehicle_is_potentially_powered) {
    0: return 0;
    return int(0.143*255);
}

switch (FEAT_TRAINS, SELF, switch_df11g_traction, num_vehs_in_vehid_chain % 6) {
    3: switch_df11g_traction_split;
    return int(0.143*255);
}


item (FEAT_TRAINS, df11g) {
    property {
        // Menu
        name:                           string(STR_NAME_DF11G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2003,11,15);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          170 km/h;
        power:                          4133 hpM;
        weight:                         138 ton;
        tractive_effort_coefficient:    0.143;
        cost_factor:                    93;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            35;
        reliability_decay:              6;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                        string(STR_DESC_5, string(STR_FULL_NAME_DF11G), string(STR_ELECTRICITY_SUPPLY_YES), string(STR_DF11G_NICKNAME), string(STR_RELDEC_VERY_LOW, 6), string(STR_DF11G_CONSIST));
        can_attach_wagon:                       locowagon;
        cargo_subtype_text:                     switch_df11g_cargo_subtype;

        // Graphics
        default:                                switch_df11g_graphics;
        purchase:                               spriteset_df11g_purchase;
        articulated_part:                       switch_df11g_articulated_part;

        // Stats
        speed:                                  switch_df11g_speed;
        purchase_speed:                         170;
        power:                                  switch_df11g_power;
        purchase_power:                         8266;
        tractive_effort_coefficient:            switch_df11g_traction;
        purchase_tractive_effort_coefficient:   int(0.143*2*255);
        length:                                 switch_length_2_6_2;
        cargo_capacity:                         0;
        cost_factor:                            93;
        running_cost_factor:                    69 * runningcostfactor() + 10;
        purchase_running_cost_factor:           276;
        purchase_cargo_capacity:                0;
/*         bitmask_vehicle_info:                   switch_df11g_bitmask_info; */
    }
}

// End DF11G

