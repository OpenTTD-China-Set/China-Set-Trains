//Begin X2K
switch (FEAT_TRAINS, SELF, switch_x2k_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_FLATCAR);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_x2k_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "x2k")
}

// Vehicle
spriteset (spriteset_x2k_empty, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k")
}
spriteset (spriteset_x2k_1_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-1-half")
}
spriteset (spriteset_x2k_1_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-1-full")
}
spriteset (spriteset_x2k_2_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-2-half")
}
spriteset (spriteset_x2k_2_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-2-full")
}
spriteset (spriteset_x2k_3_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-3-half")
}
spriteset (spriteset_x2k_3_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-3-full")
}
spriteset (spriteset_x2k_4_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-4-half")
}
spriteset (spriteset_x2k_4_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-4-full")
}
spriteset (spriteset_x2k_5_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-5-half")
}
spriteset (spriteset_x2k_5_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-5-full")
}
spriteset (spriteset_x2k_6_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-6-half")
}
spriteset (spriteset_x2k_6_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-6-full")
}
spriteset (spriteset_x2k_7_half, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-7-half")
}
spriteset (spriteset_x2k_7_full, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "x2k-7-full")
}

random_switch (FEAT_TRAINS, SELF, random_x2k_full) {
    1: spriteset_x2k_1_full; // More colours waiting to be added
    1: spriteset_x2k_2_full;
    1: spriteset_x2k_3_full;
    1: spriteset_x2k_4_full;
    1: spriteset_x2k_5_full;
    1: spriteset_x2k_6_full;
    1: spriteset_x2k_7_full;
}

random_switch (FEAT_TRAINS, SELF, random_x2k_half) {
    1: spriteset_x2k_1_half; // More colours waiting to be added
    1: spriteset_x2k_2_half;
    1: spriteset_x2k_3_half;
    1: spriteset_x2k_4_half;
    1: spriteset_x2k_5_half;
    1: spriteset_x2k_6_half;
    1: spriteset_x2k_7_half;
}

switch (FEAT_TRAINS, SELF, switch_x2k_not_empty, cargo_count > 50) {
    0: random_x2k_half;
    random_x2k_full;
}

switch (FEAT_TRAINS, SELF, switch_x2k_original, cargo_count < 5) {
    0: switch_x2k_not_empty;
    spriteset_x2k_empty;
}

switch (FEAT_TRAINS, SELF, switch_x2k_articulated_part, extra_callback_info1) {
    1..2: return x2k;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_x2k_graphics, position_in_articulated_veh % 3) {
    1: switch_x2k_original;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_x2k_capacity_cargo, cargo_type_in_veh) {
    GOOD: return 117;
    return 78;
}

switch (FEAT_TRAINS, SELF, switch_x2k_capacity, position_in_articulated_veh % 3) {
    1: switch_x2k_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_x2k_loading_speed, cargo_type_in_veh) {
    GOOD: return 18;
    return 12;
}


item (FEAT_TRAINS, x2k) {
    property {
        // Menu
        name:                           string(STR_NAME_X2K);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3095;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2004,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_EXPRESS, CC_BULK, CC_PIECE_GOODS, CC_COVERED, CC_OVERSIZED);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_LIQUID, CC_HAZARDOUS);
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [YETI, YETY];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  12 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         21.8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_X2K_LIVERY_AVAILABILITY), string(STR_FRESHNESS_MEDIUM, 185));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_x2k_graphics;
        purchase:                       spriteset_x2k_purchase;
        articulated_part:               switch_x2k_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 switch_x2k_capacity;
        loading_speed:                  switch_x2k_loading_speed() << param_loading_speed;
        cargo_age_period:               185 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    6;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        78/3;
        name:                           switch_x2k_name;

    }
}

// End X2K

