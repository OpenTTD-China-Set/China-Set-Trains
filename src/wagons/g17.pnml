//Begin G17
switch (FEAT_TRAINS, SELF, switch_g17_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_TANKER);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_g17_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "g17")
}

// Vehicle
spriteset (spriteset_g17_grey, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "g17")
}
spriteset (spriteset_g17_red, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "g17-red")
}
spriteset (spriteset_g17_yellow, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "g17-yellow")
}




random_switch (FEAT_TRAINS, SELF, random_g17_colour) {
    1: spriteset_g17_grey;
    1: spriteset_g17_red;
    1: spriteset_g17_yellow;
}
switch (FEAT_TRAINS, SELF, switch_g17_cargo_subtype, cargo_subtype) {
    0: return string(STR_RANDOM_COLOR);
    1: return string(STR_GRAY);
    2: return string(STR_RED);
    3: return string(STR_YELLOW);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_gl7_graphics_subtype, cargo_subtype) {
    0: random_g17_colour;
    1: spriteset_g17_grey;
    2: spriteset_g17_red;
    3: spriteset_g17_yellow;

}

switch (FEAT_TRAINS, SELF, switch_g17_articulated_part, extra_callback_info1) {
    1..2: return g17;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_g17_graphics, position_in_articulated_veh % 3) {
    1: switch_gl7_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_g17_capacity, position_in_articulated_veh % 3) {
    1: return 52;
    return 0;
}


item (FEAT_TRAINS, g17) {
    property {
        // Menu
        name:                           string(STR_NAME_G17);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3088;


        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1963,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_LIQUID, CC_HAZARDOUS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 52;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          0 hp;
        weight:                         22.3 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_G17_LIVERY_AVAILABILITY), string(STR_FRESHNESS_MEDIUM, 185));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_g17_cargo_subtype;

        // Graphics
        default:                        switch_g17_graphics;
        purchase:                       spriteset_g17_purchase;
        articulated_part:               switch_g17_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_g17_capacity;
        cargo_age_period:               185 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    5;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        52/3;
        name:                            switch_g17_name;

    }
}

// End G17

