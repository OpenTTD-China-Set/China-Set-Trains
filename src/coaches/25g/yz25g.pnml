// Begin YZ25G
switch (FEAT_TRAINS, SELF, switch_yz25g_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25G_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_25g_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25g")
}

// Vehicle
spriteset (spriteset_25g_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25g")
}
spriteset (spriteset_25g_jinwen, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25g-jinwen")
}
spriteset (spriteset_25g_xihu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25g-xihu")
}

switch (FEAT_TRAINS, SELF, switch_yz25g_articulated_part, extra_callback_info1) {
    1..2: return yz25g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_25g_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_RED_WHITE);
    2: return string(STR_JINWEN);
    3: return string(STR_XIHU);
    4: return string(STR_GAOZU_GREEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_25g_build_year_sprite, build_year) {
    spriteset_25g_original; // Unfinished
}

switch (FEAT_TRAINS, SELF, switch_25g_graphics_subtype, cargo_subtype) {
    0: switch_25g_build_year_sprite;
    1: spriteset_25g_original;
    2: spriteset_25g_jinwen;
    3: spriteset_25g_xihu;
    4: spriteset_25g_original;  // Will change to "spriteset_25g_gaozu" once the "gaozu green" sprite is finished
}

switch (FEAT_TRAINS, SELF, switch_25g_graphics, position_in_articulated_veh % 3) {
    1: switch_25g_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_yz25g_capacity, position_in_articulated_veh % 3) {
    1: return 118;
    return 0;
}


item (FEAT_TRAINS, yz25g) {
    property {
        // Menu
        name:                           string(STR_NAME_YZ25G);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         47.3 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_YZ25G), string(STR_25G_LIVERY_AVAILABILITY), string(STR_COMFORT_LOW, 160));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_25g_graphics;
        purchase:                       spriteset_25g_purchase_original;
        articulated_part:               switch_yz25g_articulated_part;
        cargo_subtype_text:             switch_25g_cargo_subtype;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_yz25g_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    9;
        running_cost_factor:            6 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   24;
        purchase_cargo_capacity:        118/3;
        name:                           switch_yz25g_name;
    }
}

// End YZ25G

