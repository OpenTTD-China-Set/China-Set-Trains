//Begin B22
switch (FEAT_TRAINS, SELF, switch_b22_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_REFRIGERATOR_WAGON);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_b22_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "b22")
}

// Vehicle
spriteset (spriteset_b22, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "b22")
}
spriteset (spriteset_b22_generator, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "b22-generator")
}

switch (FEAT_TRAINS, SELF, switch_b22_articulated_part, extra_callback_info1) {
    1..14: return b22;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_b22_graphics, position_in_articulated_veh % 15) {
    1: spriteset_b22;
    4: spriteset_b22;
    7: spriteset_b22_generator;
    10: spriteset_b22;
    13: spriteset_b22;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_b22_capacity_cargo, cargo_type_in_veh) {
    return 46;
}

switch (FEAT_TRAINS, SELF, switch_b22_capacity, position_in_articulated_veh % 15) {
    1: switch_b22_capacity_cargo;
    4: switch_b22_capacity_cargo;
    10: switch_b22_capacity_cargo;
    13: switch_b22_capacity_cargo;
    return 0;
}



item (FEAT_TRAINS, b22) {
    property {
        // Menu
        name:                           string(STR_NAME_B22);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3084;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1986,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       0;
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [FISH, FOOD, FRUT, FRVG, MILK, SGBT];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             VEHI;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 184;
        loading_speed:                  24 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         184 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DECAY, 1600);
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_b22_graphics;
        purchase:                       spriteset_b22_purchase;
        articulated_part:               switch_b22_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 switch_b22_capacity;
        cargo_age_period:               1600 << param_cargo_decay;
        cost_factor:                    43;
        running_cost_factor:            33 * runningcostfactor();
        purchase_running_cost_factor:   132;
        purchase_cargo_capacity:        switch_b22_capacity_cargo() * 4 / 15;
        name:                            switch_b22_name;

    }
}

// End B22

